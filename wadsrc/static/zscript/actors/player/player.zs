/*
** player.zs
**
**
**
**---------------------------------------------------------------------------
**
** Copyright 2010-2017 Christoph Oelckers
** Copyright 2017-2025 GZDoom Maintainers and Contributors
** Copyright 2025-2026 UZDoom Maintainers and Contributors
**
** SPDX-License-Identifier: GPL-3.0-or-later
**
**---------------------------------------------------------------------------
**
** Code written prior to 2026 is also licensed under:
**
** SPDX-License-Identifier: BSD-3-Clause
**
**---------------------------------------------------------------------------
**
*/

struct UserCmd native
{
	native uint	buttons;
	native int16	pitch;			// up/down
	native int16	yaw;			// left/right
	native int16	roll;			// "tilt"
	native int16	forwardmove;
	native int16	sidemove;
	native int16	upmove;
}

class PlayerPawn : Actor
{
	const CROUCHSPEED = (1./12);
	// [RH] # of ticks to complete a turn180
	const TURN180_TICKS = ((TICRATE / 4) + 1);
	// 16 pixels of bob
	const MAXBOB = 16.;

	int			crouchsprite;
	int			MaxHealth;
	int			BonusHealth;
	int			MugShotMaxHealth;
	int			MaxPickupHealth; // overrides MaxAmount of pickups and BonusHealth.
	int			RunHealth;
	private int	PlayerFlags;
	clearscope Inventory	InvFirst;		// first inventory item displayed on inventory bar
	norollback clearscope Inventory	InvSel;	// selected inventory item
	Name 		SoundClass;		// Sound class
	Name 		Portrait;
	Name 		Slot[10];
	double 		HexenArmor[5];

	// [GRB] Player class properties
	double		JumpZ;
	double		GruntSpeed;
	double		FallingScreamMinSpeed, FallingScreamMaxSpeed;
	double		ViewHeight;
	double		ForwardMove1, ForwardMove2;
	double		SideMove1, SideMove2;
	TextureID	ScoreIcon;
	int			SpawnMask;
	Name			MorphWeapon;		// This should really be a class<Weapon> but it's too late to change now.
	double		AttackZOffset;			// attack height, relative to player center
	double		UseRange;				// [NS] Distance at which player can +use
	double		AirCapacity;			// Multiplier for air supply underwater.
	Class<Inventory> FlechetteType;
	color 		DamageFade;				// [CW] Fades for when you are being damaged.
	double		FlyBob;					// [B] Fly bobbing mulitplier
	double		ViewBob;				// [SP] ViewBob Multiplier
	double		ViewBobSpeed;			// [AA] ViewBob speed multiplier
	double		WaterClimbSpeed;		// [B] Speed when climbing up walls in water
	double		FullHeight;
	double		curBob;
	double		prevBob;

	meta Name HealingRadiusType;
	meta Name InvulMode;
	meta Name Face;
	meta int TeleportFreezeTime;
	meta int ColorRangeStart;	// Skin color range
	meta int ColorRangeEnd;

	property prefix: Player;
	property HealRadiusType: HealingradiusType;
	property InvulnerabilityMode: InvulMode;
	property AttackZOffset: AttackZOffset;
	property JumpZ: JumpZ;
	property GruntSpeed: GruntSpeed;
	property FallingScreamSpeed: FallingScreamMinSpeed, FallingScreamMaxSpeed;
	property ViewHeight: ViewHeight;
	property UseRange: UseRange;
	property AirCapacity: AirCapacity;
	property MaxHealth: MaxHealth;
	property MugshotMaxHealth: MugshotMaxHealth;
	property RunHealth: RunHealth;
	property MorphWeapon: MorphWeapon;
	property FlechetteType: FlechetteType;
	property Portrait: Portrait;
	property TeleportFreezeTime: TeleportFreezeTime;
	property FlyBob: FlyBob;
	property ViewBob: ViewBob;
	property ViewBobSpeed: ViewBobSpeed;
	property WaterClimbSpeed : WaterClimbSpeed;

	flagdef NoThrustWhenInvul: PlayerFlags, 0;
	flagdef CanSuperMorph: PlayerFlags, 1;
	flagdef CrouchableMorph: PlayerFlags, 2;
	flagdef WeaponLevel2Ended: PlayerFlags, 3;
	//PF_VOODOO_ZOMBIE
	flagdef MakeFootsteps: PlayerFlags, 5; //[inkoalawetrust] Use footstep system virtual.

	enum EPrivatePlayerFlags
	{
		PF_VOODOO_ZOMBIE = 1<<4,
	}

	Default
	{
		Health 100;
		Radius 16;
		Height 56;
		Mass 100;
		Painchance 255;
		Speed 1;
		+SOLID
		+SHOOTABLE
		+DROPOFF
		+PICKUP
		+NOTDMATCH
		+FRIENDLY
		+SLIDESONWALLS
		+CANPASS
		+CANPUSHWALLS
		+FLOORCLIP
		+WINDTHRUST
		+TELESTOMP
		+NOBLOCKMONST
		Player.AttackZOffset 8;
		Player.JumpZ 8;
		Player.GruntSpeed 12;
		Player.FallingScreamSpeed 35,40;
		Player.ViewHeight 41;
		Player.UseRange 64;
		Player.ForwardMove 1,1;
		Player.SideMove 1,1;
		Player.ColorRange 0,0;
		Player.SoundClass "player";
		Player.DamageScreenColor "ff 00 00";
		Player.MugShotMaxHealth 0;
		Player.FlechetteType "ArtiPoisonBag3";
		Player.AirCapacity 1;
		Player.FlyBob 1;
		Player.ViewBob 1;
		Player.ViewBobSpeed 20;
		Player.WaterClimbSpeed 3.5;
		Player.TeleportFreezeTime 18;
		Obituary "$OB_MPDEFAULT";
	}

	//===========================================================================
	//
	// PlayerPawn :: Tick
	//
	//===========================================================================

	override void Tick()
	{
		if (player != NULL && player.mo == self && CanCrouch() && player.playerstate != PST_DEAD)
		{
			Height = FullHeight * player.crouchfactor;
		}
		else
		{
			if (health > 0) Height = FullHeight;
		}

		if (player && bWeaponLevel2Ended && !(player.cheats & CF_PREDICTING))
		{
			bWeaponLevel2Ended = false;
			if (player.ReadyWeapon != NULL && player.ReadyWeapon.bPowered_Up)
			{
				player.ReadyWeapon.EndPowerup ();
			}
			if (player.OffhandWeapon != NULL && player.OffhandWeapon.bPowered_Up)
			{
				player.OffhandWeapon.EndPowerup ();
			}
			if (player.PendingWeapon != NULL && player.PendingWeapon != WP_NOCHANGE &&
				player.PendingWeapon.bPowered_Up &&
				player.PendingWeapon.SisterWeapon != NULL)
			{
				player.PendingWeapon = player.PendingWeapon.SisterWeapon;
			}
		}
		Super.Tick();
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override void BeginPlay()
	{
		// Force create this since players can predict.
		SetViewPos((0.0, 0.0, 0.0));

		Super.BeginPlay ();
		ChangeStatNum (STAT_PLAYER);
		FullHeight = Height;
		if (!SetupCrouchSprite(crouchsprite)) crouchsprite = 0;
	}

	//===========================================================================
	//
	// PlayerPawn :: PostBeginPlay
	//
	//===========================================================================

	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		WeaponSlots.SetupWeaponSlots(self);

		// Voodoo dolls: restore original floorz/ceilingz logic
		if (player == NULL || player.mo != self)
		{
			FindFloorCeiling(FFCF_ONLYSPAWNPOS|FFCF_NOPORTALS);
			SetZ(floorz);
			FindFloorCeiling(FFCF_ONLYSPAWNPOS);
		}
		else
		{
			player.SendPitchLimits();
		}
	}

	//===========================================================================
	//
	// PlayerPawn :: MarkPrecacheSounds
	//
	//===========================================================================

	override void MarkPrecacheSounds()
	{
		Super.MarkPrecacheSounds();
		MarkPlayerSounds();
	}

	//----------------------------------------------------------------------------
	//
	//
	//
	//----------------------------------------------------------------------------

	virtual void PlayIdle ()
	{
		if (InStateSequence(CurState, SeeState))
			SetState (SpawnState);
	}

	virtual void PlayRunning ()
	{
		if (InStateSequence(CurState, SpawnState) && SeeState != NULL)
			SetState (SeeState);
	}

	virtual void PlayAttacking ()
	{
		if (MissileState != null) SetState (MissileState);
	}

	virtual void PlayAttacking2 ()
	{
		if (MeleeState != null) SetState (MeleeState);
	}

	virtual void MorphPlayerThink()
	{
	}

	//----------------------------------------------------------------------------
	//
	//
	//
	//----------------------------------------------------------------------------

	virtual void OnRespawn()
	{
		if (sv_respawnprotect && (deathmatch || alwaysapplydmflags))
		{
			let invul = Powerup(Spawn("PowerInvulnerable"));
			invul.EffectTics = 3 * TICRATE;
			invul.BlendColor = 0;			// don't mess with the view
			invul.bUndroppable = true;		// Don't drop self
			if (!invul.CallTryPickup(self))
			{
				invul.Destroy();
				return;
			}
			bRespawnInvul = true;			// [RH] special effect
		}
	}

	//----------------------------------------------------------------------------
	//
	//
	//
	//----------------------------------------------------------------------------

	override String GetObituary(Actor victim, Actor inflictor, Name mod, bool playerattack)
	{
		if (victim.player != player && victim.IsTeammate(self))
		{
			victim = self;
			return String.Format("$OB_FRIENDLY%d", random[Obituary](1, 4));
		}
		else
		{
			if (mod == 'Telefrag') return "$OB_MPTELEFRAG";

			String message;
			if (inflictor != NULL && inflictor != self)
			{
				message = inflictor.GetObituary(victim, inflictor, mod, playerattack);
			}
			if (message.Length() == 0 && playerattack && player.ReadyWeapon != NULL)
			{
				message = player.ReadyWeapon.GetObituary(victim, inflictor, mod, playerattack);
			}
			if (message.Length() == 0)
			{
				if (mod == 'BFGSplash') return "$OB_MPBFG_SPLASH";
				if (mod == 'Railgun') return "$OB_RAILGUN";
				message = Obituary;
			}
			return message;
		}
	}

	override String GetSelfObituary(Actor inflictor, Name mod)
	{
		String message;

		if (inflictor && inflictor != self)
		{
			message = inflictor.GetSelfObituary(inflictor, mod);
		}
		if (message.Length() == 0)
		{
			message = SelfObituary;
		}

		return message;
	}

	//----------------------------------------------------------------------------
	//
	// This is for SBARINFO.
	//
	//----------------------------------------------------------------------------

	clearscope int, int GetEffectTicsForItem(class<Inventory> item) const
	{
		let pg = (class<PowerupGiver>)(item);
		if (pg != null)
		{
			let powerupType = (class<Powerup>)(GetDefaultByType(pg).PowerupType);
			let powerup = Powerup(FindInventory(powerupType));
			if(powerup != null)
			{
				let maxtics = GetDefaultByType(pg).EffectTics;
				if (maxtics == 0) maxtics = powerup.default.EffectTics;
				return powerup.EffectTics, maxtics;
			}
		}
		return -1, -1;
	}

	//===========================================================================
	//
	// PlayerPawn :: CheckWeaponSwitch
	//
	// Checks if weapons should be changed after picking up ammo
	//
	//===========================================================================

	void CheckWeaponSwitch(Class<Ammo> ammotype)
	{
		let player = self.player;
		if (!player.GetNeverSwitch() &&	player.PendingWeapon == WP_NOCHANGE)
		{
			if (player.ReadyWeapon == NULL || player.ReadyWeapon.bWimpy_Weapon)
			{
				let best = BestWeapon (ammotype);
				if (best != NULL && !best.bNoAutoSwitchTo && 
					(player.ReadyWeapon == NULL || best.SelectionOrder < player.ReadyWeapon.SelectionOrder))
				{
					player.PendingWeapon = best;
				}
			}
			else if (player.OffhandWeapon == NULL || player.OffhandWeapon.bWimpy_Weapon)
			{
				let best = BestWeapon (ammotype, 1);
				if (best != NULL && !best.bNoAutoSwitchTo && 
					(player.OffhandWeapon == NULL || best.SelectionOrder < player.OffhandWeapon.SelectionOrder))
				{
					player.PendingWeapon = best;
				}
			}
		}
	}

	//---------------------------------------------------------------------------
	//
	// PROC P_FireWeapon
	//
	//---------------------------------------------------------------------------

	virtual void FireWeapon (State stat, int hand = 0)
	{
		let player = self.player;

		if (hand == 1 && player.WeaponState & WF_TWOHANDSTABILIZED)
		{
			return;
		}

		let weapn = hand ? player.OffhandWeapon : player.ReadyWeapon;
		if (weapn == null || !weapn.CheckAmmo (Weapon.PrimaryFire, true))
		{
			return;
		}

		player.WeaponState &= ~(hand ? WF_OFFHANDBOBBING : WF_WEAPONBOBBING);
		PlayAttacking ();
		weapn.bAltFire = false;
		if (stat == null)
		{
			stat = weapn.GetAtkState(!!player.refire);
		}
		player.SetPsprite(hand ? PSP_OFFHANDWEAPON : PSP_WEAPON, stat);
		if (!weapn.bNoAlert)
		{
			SoundAlert (self, false);
		}
	}

	//---------------------------------------------------------------------------
	//
	// PROC P_FireWeaponAlt
	//
	//---------------------------------------------------------------------------

	virtual void FireWeaponAlt (State stat, int hand = 0)
	{
		if (hand == 1 && player.WeaponState & WF_TWOHANDSTABILIZED)
		{
			return;
		}

		let weapn = hand ? player.OffhandWeapon : player.ReadyWeapon;
		if (weapn == null || weapn.FindState('AltFire') == null || !weapn.CheckAmmo (Weapon.AltFire, true))
		{
			return;
		}

		player.WeaponState &= ~(hand ? WF_OFFHANDBOBBING : WF_WEAPONBOBBING);
		PlayAttacking ();
		weapn.bAltFire = true;

		if (stat == null)
		{
			stat = weapn.GetAltAtkState(!!player.refire);
		}

		player.SetPsprite(hand ? PSP_OFFHANDWEAPON : PSP_WEAPON, stat);
		if (!weapn.bNoAlert)
		{
			SoundAlert (self, false);
		}
	}

	//---------------------------------------------------------------------------
	//
	// PROC P_CheckWeaponFire
	//
	// The player can fire the weapon.
	// [RH] This was in A_WeaponReady before, but that only works well when the
	// weapon's ready frames have a one tic delay.
	//
	//---------------------------------------------------------------------------

	bool CheckWeaponFire (int hand = 0)
	{
		let player = self.player;
		let weapon = hand ? player.OffhandWeapon : player.ReadyWeapon;
		
		if (weapon == NULL)
			return false;

		int ready_state = hand ? WF_OFFHANDREADY : WF_WEAPONREADY;
		int alt_state = hand ? WF_OFFHANDREADYALT : WF_WEAPONREADYALT;
		int bt_attack = hand ? BT_OFFHANDATTACK : BT_ATTACK;
		int bt_altattack = hand ? BT_OFFHANDALTATTACK : BT_ALTATTACK;
		bool wasAttackDown = hand ? player.ohattackdown : player.attackdown;
		bool attackdown = false;

		// Check for fire. Some weapons do not auto fire.
		if ((player.WeaponState & ready_state) && (player.cmd.buttons & bt_attack))
		{
			if (!wasAttackDown || !weapon.bNoAutofire)
			{
				attackdown = true;
				FireWeapon (NULL, hand);
			}
		}
		else if ((player.WeaponState & alt_state) && (player.cmd.buttons & bt_altattack))
		{
			if (!wasAttackDown || !weapon.bNoAutofire)
			{
				attackdown = true;
				FireWeaponAlt (NULL, hand);
			}
		}
		return attackdown;
	}

	//---------------------------------------------------------------------------
	//
	// PROC P_CheckWeaponChange
	//
	// The player can change to another weapon at self time.
	// [GZ] This was cut from P_CheckWeaponFire.
	//
	//---------------------------------------------------------------------------

	virtual void CheckWeaponChange ()
	{
		let player = self.player;
		if (!player) return;
		int hand = 0;

		if (player.PendingWeapon != NULL && player.PendingWeapon != WP_NOCHANGE)
		{
			hand = player.PendingWeapon.bOffhandWeapon ? 1 : 0;
			Weapon weap = hand ? player.OffhandWeapon : player.ReadyWeapon;
			if (weap == null)
			{
				player.mo.BringUpWeapon();
				return;
			}
		}
		
		// Put the weapon away if the player has a pending weapon or has died, and
		// we're at a place in the state sequence where dropping the weapon is okay.
		if ((player.PendingWeapon != WP_NOCHANGE || player.health <= 0))
		{
			int disableswitch = hand ? WF_OFFHANDDISABLESWITCH : WF_DISABLESWITCH;
			int switchok = hand ? WF_OFFHANDSWITCHOK : WF_WEAPONSWITCHOK;
			if ((player.WeaponState & disableswitch) || // Weapon changing has been disabled.
				Alternative)					// Morphed classes cannot change weapons.
			{ // ...so throw away any pending weapon requests.
				player.PendingWeapon = WP_NOCHANGE;
				return;
			}
			if (!(player.WeaponState & switchok))
			{
				return;
			}
			DropWeapon(hand);
		}
	}

	//------------------------------------------------------------------------
	//
	// PROC P_MovePsprites
	//
	// Called every tic by player thinking routine
	//
	//------------------------------------------------------------------------


	virtual void TickPSprites()
	{
		let player = self.player;
		let pspr = player.psprites;
		while (pspr)
		{
			// Destroy the psprite if it's from a weapon that isn't currently selected by the player
			// or if it's from an inventory item that the player no longer owns.
			if ((pspr.Caller == null ||
				(pspr.Caller is "Inventory" && Inventory(pspr.Caller).Owner != pspr.Owner.mo) ||
				(pspr.Caller is "Weapon" && (pspr.Caller != pspr.Owner.ReadyWeapon && pspr.Caller != pspr.Owner.OffhandWeapon))) ||
				(pspr.ID == PSP_WEAPON && pspr.Caller != pspr.Owner.ReadyWeapon) ||
				(pspr.ID == PSP_OFFHANDWEAPON && pspr.Caller != pspr.Owner.OffhandWeapon))
			{
				pspr.Destroy();
			}
			else
			{
				pspr.Tick();
			}

			pspr = pspr.Next;
		}

		if ((health > 0) || 
			(player.ReadyWeapon != null && !player.ReadyWeapon.bNoDeathInput) ||
			(player.OffhandWeapon != null && !player.OffhandWeapon.bNoDeathInput))
		{
			{
				CheckWeaponChange();
				if (player.WeaponState & (WF_WEAPONREADY | WF_WEAPONREADYALT | WF_OFFHANDREADY | WF_OFFHANDREADYALT))
				{
					player.attackdown = CheckWeaponFire(0);
					player.ohattackdown = CheckWeaponFire(1);
				}
				// Check custom buttons
				CheckWeaponButtons(0);  // check mainhand
				CheckWeaponButtons(1);  // check offhand
			}
		}
	}

	/*
	==================
	=
	= P_CalcHeight
	=
	=
	Calculate the walking / running height adjustment
	=
	==================
	*/

	virtual void CalcHeight()
	{
		let player = self.player;
		double angle;
		double bob;
		bool still = false;

		// Regular movement bobbing
		// (needs to be calculated for gun swing even if not on ground)

		// killough 10/98: Make bobbing depend only on player-applied motion.
		//
		// Note: don't reduce bobbing here if on ice: if you reduce bobbing here,
		// it causes bobbing jerkiness when the player moves from ice to non-ice,
		// and vice-versa.

		if (player.cheats & CF_NOCLIP2)
		{
			player.bob = 0;
		}
		else if (bNoGravity && !player.onground)
		{
			player.bob = min(abs(0.5 * FlyBob), MAXBOB);
		}
		else
		{
			player.bob = player.Vel dot player.Vel;
			if (player.bob == 0)
			{
				still = true;
			}
			else
			{
				player.bob *= player.GetMoveBob();

				if (player.bob > MAXBOB)
					player.bob = MAXBOB;
			}
		}

		double defaultviewheight = ViewHeight + player.crouchviewdelta;

		if (player.cheats & CF_NOVELOCITY)
		{
			player.viewz = pos.Z + defaultviewheight;

			if (player.viewz > ceilingz-4)
				player.viewz = ceilingz-4;

			return;
		}

		if (bFly && !GetCVar("FViewBob"))
		{
			bob = 0;
		}
		else if (still)
		{
			if (player.health > 0)
			{
				angle = player.BobTimer / (120 * TICRATE / 35.) * 360.;
				bob = player.GetStillBob() * sin(angle);
			}
			else
			{
				bob = 0;
			}
		}
		else
		{
			angle = player.BobTimer / (ViewBobSpeed * TICRATE / 35.) * 360.;
			bob = player.bob * sin(angle) * (waterlevel > 1 ? 0.25f : 0.5f);
		}

		// move viewheight
		if (player.playerstate == PST_LIVE)
		{
			player.viewheight += player.deltaviewheight;

			if (player.viewheight > defaultviewheight)
			{
				player.viewheight = defaultviewheight;
				player.deltaviewheight = 0;
			}
			else if (player.viewheight < (defaultviewheight/2))
			{
				player.viewheight = defaultviewheight/2;
				if (player.deltaviewheight <= 0)
					player.deltaviewheight = double.equal_epsilon;
			}

			if (player.deltaviewheight)
			{
				player.deltaviewheight += 0.25;
				if (!player.deltaviewheight)
					player.deltaviewheight = double.equal_epsilon;
			}
		}

		if (Alternative)
		{
			bob = 0;
		}
		player.viewz = pos.Z + player.viewheight + (bob * clamp(ViewBob, 0. , 1.5)); // [SP] Allow DECORATE changes to view bobbing speed.

		if (Floorclip && player.playerstate != PST_DEAD
			&& pos.Z <= floorz + 2)
		{
			player.viewz -= Floorclip;
		}
		if (player.viewz > ceilingz - 4)
		{
			player.viewz = ceilingz - 4;
		}
		if (player.viewz < floorz + 4)
		{
			player.viewz = floorz + 4;
		}
	}

	//==========================================================================
	//
	// P_DeathThink
	//
	//==========================================================================

	virtual void DeathThink ()
	{
		let player = self.player;
		int dir;
		double delta;

		player.Uncrouch();
		TickPSprites();

		player.onground = (pos.Z <= floorz + 2);
		if (self is "PlayerChunk")
		{ // Flying bloody skull or flying ice chunk
			player.viewheight = 6;
			player.deltaviewheight = 0;
			if (player.onground)
			{
				if (Pitch > -19.)
				{
					double lookDelta = (-19. - Pitch) / 8;
					Pitch += lookDelta;
				}
			}
		}
		else if (!bIceCorpse)
		{ // Fall to ground (if not frozen)
			player.deltaviewheight = 0;
			if (player.viewheight > 6)
			{
				player.viewheight -= 1;
			}
			if (player.viewheight < 6)
			{
				player.viewheight = 6;
			}
			if (Pitch < 0)
			{
				Pitch += 3;
			}
			else if (Pitch > 0)
			{
				Pitch -= 3;
			}
			if (abs(Pitch) < 3)
			{
				Pitch = 0.;
			}
		}
		player.mo.CalcHeight ();

		if (player.attacker && player.attacker != self)
		{ // Watch killer
			double diff = deltaangle(angle, AngleTo(player.attacker));
			double delta = abs(diff);

			if (delta < 10)
			{ // Looking at killer, so fade damage and poison counters
				if (player.damagecount)
				{
					player.damagecount--;
				}
				if (player.poisoncount)
				{
					player.poisoncount--;
				}
			}
			delta /= 8;
			Angle += clamp(diff, -5., 5.);
		}
		else
		{
			if (player.damagecount)
			{
				player.damagecount--;
			}
			if (player.poisoncount)
			{
				player.poisoncount--;
			}
		}

		if ((player.cmd.buttons & BT_USE ||
			((deathmatch || alwaysapplydmflags) && sv_forcerespawn)) && !sv_norespawn)
		{
			if (Level.maptime >= player.respawn_time || ((player.cmd.buttons & BT_USE) && player.Bot == NULL))
			{
				player.cls = NULL;		// Force a new class if the player is using a random class
				player.playerstate = (multiplayer || level.AllowRespawn || sv_singleplayerrespawn || G_SkillPropertyInt(SKILLP_PlayerRespawn)) ? PST_REBORN : PST_ENTER;
				if (special1 > 2)
				{
					special1 = 0;
				}
			}
		}
	}

	//===========================================================================
	//
	// PlayerPawn :: Die
	//
	//===========================================================================

	override void Die (Actor source, Actor inflictor, int dmgflags, Name MeansOfDeath)
	{
		Super.Die (source, inflictor, dmgflags, MeansOfDeath);

		if (player != NULL && player.mo == self)
		{
			PlayerDiedMakeRumble(inflictor);
			player.bonuscount = 0;
		}

		// [RL0] To allow voodoo zombies, don't kill the player together with voodoo dolls if the compat flag is enabled
		if (player != NULL && player.mo != self && !(Level.compatflags2 & COMPATF2_VOODOO_ZOMBIES))
		{ // Make the real player die, too
			player.mo.Die (source, inflictor, dmgflags, MeansOfDeath);
		}
		else
		{
			// [RL0] player.mo == self will always be true if COMPATF2_VOODOO_ZOMBIES is false, so there's no need to check the compatflag here too, just self
			if (player != NULL && sv_weapondrop && player.mo == self)
			{ // Voodoo dolls don't drop weapons
				let weap = player.ReadyWeapon;
				if (weap != NULL)
				{
					// kgDROP - start - modified copy from a_action.cpp
					let di = weap.GetDropItems();

					if (di != NULL)
					{
						while (di != NULL)
						{
							if (di.Name != 'None')
							{
								class<Actor> ti = di.Name;
								if (ti) A_DropItem (ti, di.Amount, di.Probability);
							}
							di = di.Next;
						}
					}
					else if (weap.SpawnState != NULL &&
						weap.SpawnState != GetDefaultByType('Actor').SpawnState)
					{
						let weapitem = Weapon(A_DropItem (weap.GetClass(), -1, 256));
						if (weapitem)
						{
							if (weap.AmmoGive1 && weap.Ammo1)
							{
								weapitem.AmmoGive1 = weap.Ammo1.Amount;
							}
							if (weap.AmmoGive2 && weap.Ammo2)
							{
								weapitem.AmmoGive2 = weap.Ammo2.Amount;
							}
							weapitem.bIgnoreSkill = true;
						}
					}
					else
					{
						let item = Inventory(A_DropItem (weap.AmmoType1, -1, 256));
						if (item != NULL)
						{
							item.Amount = weap.Ammo1.Amount;
							item.bIgnoreSkill = true;
						}
						item = Inventory(A_DropItem (weap.AmmoType2, -1, 256));
						if (item != NULL)
						{
							item.Amount = weap.Ammo2.Amount;
							item.bIgnoreSkill = true;
						}
					}
				}
			}
			if (!multiplayer && level.deathsequence != 'None')
			{
				level.StartIntermission(level.deathsequence, FSTATE_EndingGame);
			}
		}
	}

	//===========================================================================
	//
	// PlayerPawn :: FilterCoopRespawnInventory
	//
	// When respawning in coop, this function is called to walk through the dead
	// player's inventory and modify it according to the current game flags so
	// that it can be transferred to the new live player. This player currently
	// has the default inventory, and the oldplayer has the inventory at the time
	// of death.
	//
	//===========================================================================

	virtual void FilterCoopRespawnInventory (PlayerPawn oldplayer, Weapon curHeldWeapon = null)
	{
		// If we're losing everything, this is really simple.
		if (sv_cooploseinventory)
		{
			oldplayer.DestroyAllInventory();
			return;
		}

		// Make sure to get the real held weapon before messing with the inventory.
		if (curHeldWeapon && curHeldWeapon.bPowered_Up)
			curHeldWeapon = curHeldWeapon.SisterWeapon;

		// Walk through the old player's inventory and destroy or modify
		// according to dmflags.
		Inventory next;
		for (Inventory item = oldplayer.Inv; item != NULL; item = next)
		{
			next = item.Inv;

			// If this item is part of the default inventory, we never want
			// to destroy it, although we might want to copy the default
			// inventory amount.
			let defitem = FindInventory (item.GetClass());

			if ((sv_cooplosekeys && !sv_coopsharekeys) && defitem == NULL && item is 'Key')
			{
				item.Destroy();
			}
			else if (sv_cooploseweapons && defitem == NULL && item is 'Weapon')
			{
				item.Destroy();
			}
			else if (sv_cooplosearmor && item is 'Armor')
			{
				if (defitem == NULL)
				{
					item.Destroy();
				}
				else if (item is 'BasicArmor')
				{
					BasicArmor(item).SavePercent = BasicArmor(defitem).SavePercent;
					item.Amount = defitem.Amount;
				}
				else if (item is 'HexenArmor')
				{
					let to = HexenArmor(item);
					let from = HexenArmor(defitem);
					to.Slots[0] = from.Slots[0];
					to.Slots[1] = from.Slots[1];
					to.Slots[2] = from.Slots[2];
					to.Slots[3] = from.Slots[3];
				}
			}
			else if (sv_cooplosepowerups &&	defitem == NULL && item is  'Powerup')
			{
				item.Destroy();
			}
			else if ((sv_cooploseammo || sv_coophalveammo) && item is 'Ammo')
			{
				if (defitem == NULL)
				{
					if (sv_cooploseammo)
					{
						// Do NOT destroy the ammo, because a weapon might reference it.
						item.Amount = 0;
					}
					else if (item.Amount > 1)
					{
						item.Amount /= 2;
					}
				}
				else
				{
					// When set to lose ammo, you get to keep all your starting ammo.
					// When set to halve ammo, you won't be left with less than your starting amount.
					if (sv_cooploseammo)
					{
						item.Amount = defitem.Amount;
					}
					else if (item.Amount > 1)
					{
						item.Amount = MAX(item.Amount / 2, defitem.Amount);
					}
				}
			}
		}

		// Now destroy the default inventory this player is holding and move
		// over the old player's remaining inventory.
		DestroyAllInventory();
		ObtainInventory (oldplayer);

		player.ReadyWeapon = player.OffhandWeapon = NULL;
		if (curHeldWeapon && curHeldWeapon.owner == self && curHeldWeapon.CheckAmmo(Weapon.EitherFire, false))
		{
			player.PendingWeapon = curHeldWeapon;
			BringUpWeapon();
		}
		else
		{
			PickNewWeapon (NULL);
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckFOV
	//
	//----------------------------------------------------------------------------

	virtual void CheckFOV()
	{
		let player = self.player;

		if (!player) return;

		double fov1 = player.ReadyWeapon != NULL ? player.ReadyWeapon.FOVScale : 0;
		double fov2 = player.OffhandWeapon != NULL ? player.OffhandWeapon.FOVScale : 0;
		double fovscale = MAX(fov1, fov2);

		// [RH] Zoom the player's FOV
		float desired = player.DesiredFOV;
		// Adjust FOV using on the currently held weapon.
		if (player.playerstate != PST_DEAD &&		// No adjustment while dead.
			fovscale != 0)		// No adjustment if the adjustment is zero.
		{
			// A negative scale is used to prevent G_AddViewAngle/G_AddViewPitch
			// from scaling with the FOV scale.
			desired *= abs(fovscale);
		}
		if (player.FOV != desired)
		{
			if (abs(player.FOV - desired) < 7.)
			{
				player.FOV = desired;
			}
			else
			{
				float zoom = MAX(7., abs(player.FOV - desired) * 0.025);
				if (player.FOV > desired)
				{
					player.FOV = player.FOV - zoom;
				}
				else
				{
					player.FOV = player.FOV + zoom;
				}
			}
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckCheats
	//
	//----------------------------------------------------------------------------

	virtual void CheckCheats()
	{
		let player = self.player;
		// No-clip cheat
		if ((player.cheats & (CF_NOCLIP | CF_NOCLIP2)) == CF_NOCLIP2)
		{ // No noclip2 without noclip
			player.cheats &= ~CF_NOCLIP2;
		}
		bNoClip = (player.cheats & (CF_NOCLIP | CF_NOCLIP2) || Default.bNoClip);
		if (player.cheats & CF_NOCLIP2)
		{
			bNoGravity = true;
		}
		else if (!bFly && !Default.bNoGravity)
		{
			bNoGravity = false;
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckFrozen
	//
	//----------------------------------------------------------------------------

	virtual bool CheckFrozen()
	{
		let player = self.player;
		UserCmd cmd = player.cmd;
		bool totallyfrozen = player.IsTotallyFrozen();

		// [RH] Being totally frozen zeros out most input parameters.
		if (totallyfrozen)
		{
			if (gamestate == GS_TITLELEVEL)
			{
				cmd.buttons = 0;
			}
			else
			{
				cmd.buttons &= BT_USE;
			}
			//cmd.pitch = 0;
			//cmd.yaw = 0;
			//cmd.roll = 0;
			cmd.forwardmove = 0;
			cmd.sidemove = 0;
			cmd.upmove = 0;
			player.turnticks = 0;
		}
		else if (player.cheats & CF_FROZEN)
		{
			cmd.forwardmove = 0;
			cmd.sidemove = 0;
			cmd.upmove = 0;
		}
		return totallyfrozen;
	}

	virtual bool CanCrouch() const
	{
		return !Alternative || bCrouchableMorph;
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CrouchMove
	//
	//----------------------------------------------------------------------------

	virtual void CrouchMove(int direction)
	{
		let player = self.player;

		double defaultheight = FullHeight;
		double savedheight = Height;
		double crouchspeed = direction * CROUCHSPEED;
		double oldheight = player.viewheight;

		player.crouchdir = direction;
		if (direction != 0)
		{
			player.crouchfactor += crouchspeed;
		}

		// check whether the move is ok
		Height  = defaultheight * player.crouchfactor;
		if (!TryMove(Pos.XY, false, NULL))
		{
			Height = savedheight;
			if (direction > 0)
			{
				// doesn't fit
				player.crouchfactor -= crouchspeed;
				return;
			}
		}
		Height = savedheight;

		if (direction != 0)
		{  // clamp when using crouch with button only
			player.crouchfactor = clamp(player.crouchfactor, 0.5, 1.);
		}
		player.viewheight = ViewHeight * player.crouchfactor;
		player.crouchviewdelta = player.viewheight - ViewHeight;

		// Check for eyes going above/below fake floor due to crouching motion.
		CheckFakeFloorTriggers(pos.Z + oldheight, true);
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckCrouch
	//
	//----------------------------------------------------------------------------

	virtual void CheckCrouch(bool totallyfrozen)
	{
		let player = self.player;
		UserCmd cmd = player.cmd;

		if (cmd.buttons & BT_JUMP)
		{
			cmd.buttons &= ~BT_CROUCH;
		}
		if (CanCrouch() && player.health > 0 && level.IsCrouchingAllowed())
		{
			if (!totallyfrozen)
			{
				int crouchdir = player.crouching;

				if (player.crouching == 10)
				{
					CrouchMove(0);
				}
				else if (crouchdir == 0)
				{
					crouchdir = (cmd.buttons & BT_CROUCH) ? -1 : 1;
				}
				else if (cmd.buttons & BT_CROUCH)
				{
					player.crouching = 0;
				}
				if (crouchdir == 1 && player.crouchfactor < 1 && pos.Z + height < ceilingz)
				{
					CrouchMove(1);
				}
				else if (crouchdir == -1 && player.crouchfactor > 0.5)
				{
					CrouchMove(-1);
				}
			}
		}
		else
		{
			player.Uncrouch();
		}

		player.crouchoffset = -(ViewHeight) * (1 - player.crouchfactor);
	}

	//----------------------------------------------------------------------------
	//
	// P_Thrust
	//
	// moves the given origin along a given angle
	//
	//----------------------------------------------------------------------------

	void ForwardThrust (double move, double angle)
	{
		if ((waterlevel || bNoGravity) && Pitch != 0 && !player.GetClassicFlight())
		{
			double zpush = move * sin(Pitch);
			if (waterlevel && waterlevel < 2 && zpush < 0) zpush = 0;
			Vel.Z -= zpush;
			move *= cos(Pitch);
		}
		Thrust(move, angle);
	}

	//----------------------------------------------------------------------------
	//
	// P_Bob
	// Same as P_Thrust, but only affects bobbing.
	//
	// killough 10/98: We apply thrust separately between the real physical player
	// and the part which affects bobbing. This way, bobbing only comes from player
	// motion, nothing external, avoiding many problems, e.g. bobbing should not
	// occur on conveyors, unless the player walks on one, and bobbing should be
	// reduced at a regular rate, even on ice (where the player coasts).
	//
	//----------------------------------------------------------------------------

	void Bob (double angle, double move, bool forward)
	{
		if (forward && (waterlevel || bNoGravity) && Pitch != 0)
		{
			move *= cos(Pitch);
		}
		player.Vel += AngleToVector(angle, move);
	}

	//===========================================================================
	//
	// PlayerPawn :: TweakSpeeds
	//
	//===========================================================================

	virtual double, double TweakSpeeds (double forward, double side)
	{
		// Strife's player can't run when its health is below 10
		if (health <= RunHealth)
		{
			forward = clamp(forward, -gameinfo.normforwardmove[0]*256, gameinfo.normforwardmove[0]*256);
			side = clamp(side, -gameinfo.normsidemove[0]*256, gameinfo.normsidemove[0]*256);
		}

		// [GRB]
		if (abs(forward) < 0x3200)
		{
			forward *= ForwardMove1;
		}
		else
		{
			forward *= ForwardMove2;
		}

		if (abs(side) < 0x2800)
		{
			side *= SideMove1;
		}
		else
		{
			side *= SideMove2;
		}

		if (!Alternative)
		{
			double factor = 1.;
			for(let it = Inv; it != null; it = it.Inv)
			{
				factor *= it.GetSpeedFactor ();
			}
			forward *= factor;
			side *= factor;
		}
		return forward, side;
	}

	virtual void ApplyAirControl(out double movefactor, out double bobfactor)
	{
		movefactor *= level.aircontrol;
		bobfactor *= level.aircontrol;
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_MovePlayer
	//
	//----------------------------------------------------------------------------

	virtual void MovePlayer ()
	{
		let player = self.player;
		UserCmd cmd = player.cmd;
		player.resetDoomYaw = false;

		// [RH] 180-degree turn overrides all other yaws
		if (player.turnticks)
		{
			if (player.PlayInVR && !multiplayer)
			{
				player.turnticks = 0;
				Angle += 180.;
				player.resetDoomYaw = true;
			}
			else
			{
				player.turnticks--;
				A_SetAngle(Angle + (180. / TURN180_TICKS), SPF_INTERPOLATE);
			}
		}
		else
		{
			Angle += cmd.yaw * (360./65536.);
		}

		// only turning possible when player frozen
		if (reactiontime) return;

		player.onground = (pos.z <= floorz + 2) || bOnMobj || bMBFBouncer || (player.cheats & CF_NOCLIP2);

		double friction, movefactor;
		[friction, movefactor] = GetFriction();
		//Taken from the Wolf-3D TC - Prevent player having momentum/acceleration to avoid puking
		if (!multiplayer && !vr_momentum && !player.keepmomentum
		&& player.onground && friction == ORIG_FRICTION)
		{
			vel.XY *= 0.0001;
			Speed = Default.Speed * 8;
		}
		else
		{
			Speed = Default.Speed;
		}

		if (!multiplayer && abs(vel.x) < vr_momentum_threshold && abs(vel.y) < vr_momentum_threshold)
		{
			player.keepmomentum = false;
		}

		// killough 10/98:
		//
		// We must apply thrust to the player and bobbing separately, to avoid
		// anomalies. The thrust applied to bobbing is always the same strength on
		// ice, because the player still "works just as hard" to move, while the
		// thrust applied to the movement varies with 'movefactor'.

		if (cmd.forwardmove | cmd.sidemove)
		{
			double forwardmove, sidemove;
			double bobfactor;
			double fm, sm;

			bobfactor = friction < ORIG_FRICTION ? movefactor : ORIG_FRICTION_FACTOR;
			if (!player.onground && !bNoGravity && !waterlevel)
			{
				// [RH] allow very limited movement if not on ground.
				// [AA] but also allow authors to override it.
				ApplyAirControl(movefactor, bobfactor);
			}

			fm = cmd.forwardmove;
			sm = cmd.sidemove;
			[fm, sm] = TweakSpeeds (fm, sm);
			fm *= Speed / 256;
			sm *= Speed / 256;

			// When crouching, speed and bobbing have to be reduced
			if (CanCrouch() && player.crouchfactor != 1)
			{
				double speedfactor = clamp(player.crouchfactor, 0.5, 1.);
				fm *= speedfactor;
				sm *= speedfactor;
				bobfactor *= speedfactor;
			}

			forwardmove = fm * movefactor * (35 / TICRATE);
			sidemove = sm * movefactor * (35 / TICRATE);

			if (forwardmove)
			{
				Bob(Angle, cmd.forwardmove * bobfactor / 256., true);
				ForwardThrust(forwardmove, Angle);
			}
			if (sidemove)
			{
				let a = Angle - 90;
				Bob(a, cmd.sidemove * bobfactor / 256., false);
				Thrust(sidemove, a);
			}

			if (!(player.cheats & CF_PREDICTING) && (forwardmove != 0 || sidemove != 0))
			{
				PlayRunning ();
			}

			if (player.cheats & CF_REVERTPLEASE)
			{
				player.cheats &= ~CF_REVERTPLEASE;
				player.camera = player.mo;
			}
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckPitch
	//
	//----------------------------------------------------------------------------

	virtual void CheckPitch()
	{
		let player = self.player;
		// [RH] Look up/down stuff
		if (!level.IsFreelookAllowed())
		{
			Pitch = 0.;
		}
		else
		{
			// The player's view pitch is clamped between -32 and +56 degrees,
			// which translates to about half a screen height up and (more than)
			// one full screen height down from straight ahead when view panning
			// is used.
			int clook = player.cmd.pitch;
			if (clook != 0)
			{
				if (clook == -32768)
				{ // center view
					player.centering = true;
				}
				else if (!player.centering)
				{
					// no more overflows with floating point. Yay! :)
					Pitch = clamp(Pitch - clook * (360. / 65536.), player.MinPitch, player.MaxPitch);
				}
			}
		}
		if (player.centering)
		{
			if (abs(Pitch) > 2.)
			{
				A_SetPitch(Pitch * (2. / 3.), SPF_INTERPOLATE);
			}
			else
			{
				A_SetPitch(Pitch * 0.75, SPF_INTERPOLATE);
				if (abs(Pitch) <= 0.25)
				{
					A_SetPitch(0., SPF_INTERPOLATE);
					player.centering = false;
					if (PlayerNumber() == consoleplayer)
						LocalViewPitch = 0;
				}
			}
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckJump
	//
	//----------------------------------------------------------------------------

	virtual void CheckJump()
	{
		let player = self.player;
		// [RH] check for jump
		if (player.cmd.buttons & BT_JUMP)
		{
			if (player.crouchfactor < 0.75)
			{
				// Jumping while crouching will force an un-crouch but not jump
				player.crouching = 1;
			}
			else if (waterlevel >= 2)
			{
				Vel.Z = 4 * Speed;
			}
			else if (bNoGravity)
			{
				Vel.Z = 3.;
			}
			else if (level.IsJumpingAllowed() && player.onground && player.jumpTics == 0)
			{
				double jumpvelz = JumpZ * 35 / TICRATE;
				double jumpfac = 0;

				// [BC] If the player has the high jump power, double his jump velocity.
				// (actually, pick the best factors from all active items.)
				for (let p = Inv; p != null; p = p.Inv)
				{
					let pp = PowerHighJump(p);
					if (pp)
					{
						double f = pp.Strength;
						if (f > jumpfac) jumpfac = f;
					}
				}
				if (jumpfac > 0) jumpvelz *= jumpfac;

				Vel.Z += jumpvelz;
				bOnMobj = false;
				player.jumpTics = -1;
				if (!(player.cheats & CF_PREDICTING)) A_StartSound("*jump", CHAN_BODY);
			}
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckMoveUpDown
	//
	//----------------------------------------------------------------------------

	virtual void CheckMoveUpDown()
	{
		let player = self.player;
		UserCmd cmd = player.cmd;

		if (cmd.upmove == -32768)
		{ // Only land if in the air
			if (bNoGravity && waterlevel < 2)
			{
				bNoGravity = false;
			}
		}
		else if (cmd.upmove != 0)
		{
			// Clamp the speed to some reasonable maximum.
			cmd.upmove = clamp(cmd.upmove, -0x300, 0x300);
			if (waterlevel >= 2 ||  bFly || (player.cheats & CF_NOCLIP2))
			{
				Vel.Z = Speed * cmd.upmove / 128.;
				if (waterlevel < 2 && !bNoGravity)
				{
					bFly = true;
					bNoGravity = true;
					if ((Vel.Z <= -39) && !(player.cheats & CF_PREDICTING))
					{ // Stop falling scream
						A_StopSound(CHAN_VOICE);
					}
				}
			}
			else if (cmd.upmove > 0 && !(player.cheats & CF_PREDICTING))
			{
				let fly = FindInventory("ArtiFly");
				if (fly != NULL)
				{
					UseInventory(fly);
				}
			}
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_HandleMovement
	//
	//----------------------------------------------------------------------------

	virtual void HandleMovement()
	{
		let player = self.player;
		// [RH] Check for fast turn around
		if (player.cmd.buttons & BT_TURN180 && !(player.oldbuttons & BT_TURN180))
		{
			player.turnticks = TURN180_TICKS;
		}

		// Handle movement
		if (reactiontime)
		{ // Player is frozen
			reactiontime--;
		}
		MovePlayer();
		if (reactiontime == 0)
		{
			CheckJump();
			CheckMoveUpDown();
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckUndoMorph
	//
	//----------------------------------------------------------------------------

	virtual void CheckUndoMorph()
	{
		let player = self.player;
		// Morph counter
		if (Alternative)
		{
			if (player.chickenPeck)
			{ // Chicken attack counter
				player.chickenPeck -= 3;
			}
			if (player.MorphTics && !--player.MorphTics)
			{ // Attempt to undo the chicken/pig
				Unmorph(self, MRF_UNDOBYTIMEOUT);
			}
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckPoison
	//
	//----------------------------------------------------------------------------

	virtual void CheckPoison()
	{
		let player = self.player;
		if (player.poisoncount && !(Level.maptime & 15))
		{
			player.poisoncount -= 5;
			if (player.poisoncount < 0)
			{
				player.poisoncount = 0;
			}
			player.PoisonDamage(player.poisoner, 1, true);
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckDegeneration
	//
	//----------------------------------------------------------------------------

	virtual void CheckDegeneration()
	{
		// Apply degeneration.
		if (sv_degeneration)
		{
			let player = self.player;
			int maxhealth = GetMaxHealth(true);
			if ((Level.maptime % TICRATE) == 0 && player.health > maxhealth)
			{
				if (player.health - 5 < maxhealth)
					player.health = maxhealth;
				else
					player.health--;

				health = player.health;
			}
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_CheckAirSupply
	//
	//----------------------------------------------------------------------------

	virtual void CheckAirSupply()
	{
		// Handle air supply
		//if (level.airsupply > 0)
		{
			let player = self.player;
			if (waterlevel < 3 || (bInvulnerable) || (player.cheats & (CF_GODMODE | CF_NOCLIP2)) ||	(player.cheats & CF_GODMODE2))
			{
				ResetAirSupply();
			}
			else if (player.air_finished <= Level.maptime && !(Level.maptime & 31))
			{
				DamageMobj(NULL, NULL, 2 + ((Level.maptime - player.air_finished) / TICRATE), 'Drowning');
			}
		}
	}

	//----------------------------------------------------------------------------
	//
	// PROC P_PlayerThink
	//
	//----------------------------------------------------------------------------

	virtual void PlayerThink()
	{
		let player = self.player;
		UserCmd cmd = player.cmd;

		// [RL0] Mark players that became zombies (this stays even if they 'revive' by healing, until a level change)
		if((Level.compatflags2 & COMPATF2_VOODOO_ZOMBIES) && player.health <= 0 && player.mo.health > 0)
		{
			PlayerFlags |= PF_VOODOO_ZOMBIE;
		}

		CheckFOV();

		CheckCheats();

		if (bJustAttacked && (!player.PlayInVR || (!multiplayer && vanilla_melee_attack)))
		{ // Chainsaw/Gauntlets attack auto forward motion
			cmd.yaw = 0;
			cmd.forwardmove = 0xc800/2;
			cmd.sidemove = 0;
			bJustAttacked = false;
		}

		bool totallyfrozen = CheckFrozen();

		// Handle crouching
		CheckCrouch(totallyfrozen);
		CheckMusicChange();

		if (player.playerstate == PST_DEAD)
		{
			DeathThink ();
			return;
		}
		if (player.jumpTics != 0)
		{
			player.jumpTics--;
			if (player.onground && player.jumpTics < -18)
			{
				player.jumpTics = 0;
			}
		}
		if (Alternative && !(player.cheats & CF_PREDICTING))
		{
			MorphPlayerThink ();
		}

		CheckPitch();
		HandleMovement();
		CalcHeight ();
		if (bMakeFootsteps) MakeFootsteps();

		if (!(player.cheats & CF_PREDICTING))
		{
			CheckEnvironment();
			// Note that after this point the PlayerPawn may have changed due to getting unmorphed or getting its skull popped so 'self' is no longer safe to use.
			// This also must not read mo into a local variable because several functions in this block can change the attached PlayerPawn.
			player.mo.CheckUse();
			player.mo.CheckUndoMorph();
			// Cycle psprites.
			player.mo.TickPSprites();
			// Other Counters
			if (player.damagecount)	player.damagecount--;
			if (player.bonuscount) player.bonuscount--;

			if (player.hazardcount)
			{
				player.hazardcount--;
				if (player.hazardinterval <= 0)
					player.hazardinterval = 32; // repair invalid hazardinterval
				if (!(Level.maptime % player.hazardinterval) && player.hazardcount > 16*TICRATE)
					player.mo.DamageMobj (NULL, NULL, 5, player.hazardtype);
			}
			player.mo.CheckPoison();
			player.mo.CheckDegeneration();
			player.mo.CheckAirSupply();
		}
	}

	//---------------------------------------------------------------------------
	//
	// Handle player footstep sounds.
	// Default footstep handling.
	//
	//---------------------------------------------------------------------------

	int footstepCounter;
	double footstepLength;
	bool footstepFoot;

	void DoFootstep(TerrainDef Ground)
	{
		Sound Step = Ground.StepSound;

		//Generic foot-agnostic sound takes precedence.
		if(!Step)
		{
			//Apparently most people walk with their right foot first, so assume that here.
			if (!footstepFoot)
			{
				Step = Ground.LeftStepSound;
			}
			else
			{
				Step = Ground.RightStepSound;
			}

			footstepFoot = !footstepFoot;
		}

		if(Step)
		{
			A_StartSound(Step, flags: CHANF_OVERLAP, volume: Ground.StepVolume * snd_footstepvolume);
		}

		//Steps make splashes regardless.
		bool Heavy = (Mass >= 200) ? 0 : THW_SMALL; //Big player makes big splash.
		HitWater(CurSector, (Pos.XY, CurSector.FloorPlane.ZatPoint(Pos.XY)), true, false, flags: Heavy | THW_NOVEL);
	}

	virtual void MakeFootsteps()
	{
		if(pos.z > floorz) return;

		let Ground = GetFloorTerrain();

		if(Ground && (player.cmd.forwardMove != 0 || player.cmd.sideMove != 0))
		{
			int Delay = (player.cmd.buttons & BT_RUN) ? Ground.RunStepTics : Ground.WalkStepTics;

			if((player.cmd.buttons ^ player.oldbuttons) & BT_RUN)
			{ // zero out counters when starting/stopping a run
				footstepCounter = 0;
				footstepLength = Ground.StepDistance;
			}

			if(Ground.StepDistance > 0)
			{ // distance-based terrain
				footstepCounter = 0;

				double moveVel = vel.xy.length();

				if(moveVel > Ground.StepDistanceMinVel)
				{
					footstepLength += moveVel;

					while(footstepLength > Ground.StepDistance)
					{
						footstepLength -= Ground.StepDistance;
						DoFootstep(Ground);
					}
				}
				else
				{
					footstepLength = Ground.StepDistance;
				}

			}
			else if(Delay > 0)
			{ // delay-based terrain
				footstepLength = 0;

				if(footstepCounter % Delay == 0)
				{
					DoFootstep(Ground);
				}

				footstepCounter = (footstepCounter + 1) % Delay;
			}
		}
		else
		{
			footstepCounter = 0;
			footstepLength = Ground.StepDistance;
			footstepFoot = false;
		}

	}

	//---------------------------------------------------------------------------
	//
	// PROC P_BringUpWeapon
	//
	// Starts bringing the pending weapon up from the bottom of the screen.
	// This is only called to start the rising, not throughout it.
	//
	//---------------------------------------------------------------------------

	void BringUpWeapon ()
	{
		// [RL0] Don't bring up weapon when in a voodoo zombie state
		if(PlayerFlags & PF_VOODOO_ZOMBIE) return;

		let player = self.player;
		if (player.PendingWeapon == WP_NOCHANGE)
		{
			if (player.ReadyWeapon != null)
			{
				let psp = player.GetPSprite(PSP_WEAPON);
				if (psp)
				{
					psp.y = WEAPONTOP;
					player.ReadyWeapon.ResetPSprite(psp);
				}
				player.SetPsprite(PSP_WEAPON, player.ReadyWeapon.GetReadyState());
			}

			if (player.OffhandWeapon != null)
			{
				let psp = player.GetPSprite(PSP_OFFHANDWEAPON);
				if (psp) 
				{
					psp.y = WEAPONTOP;
					player.OffhandWeapon.ResetPSprite(psp);
				}
				player.SetPsprite(PSP_OFFHANDWEAPON, player.OffhandWeapon.GetReadyState());
			}

			return;
		}

		let weapon = player.PendingWeapon;

		// If the player has a tome of power, use self weapon's powered up
		// version, if one is available.
		if (weapon != null &&
			weapon.SisterWeapon &&
			weapon.SisterWeapon.bPowered_Up &&
			player.mo.FindInventory ('PowerWeaponLevel2', true))
		{
			weapon = weapon.SisterWeapon;
		}

		player.PendingWeapon = WP_NOCHANGE;
		player.mo.weaponspecial = 0;

		if (weapon != null)
		{
			weapon.OnSelect();
			player.SetPsprite(PSP_FLASH, null);
			// Make the slot the single source of truth for which hand holds this
			// weapon, and clear it out of the other one.
			//
			// The engine has two ways of asking "which hand is this weapon in":
			// the Weapon.bOffhandWeapon flag (read by ~41 sites, including the
			// LAF_ISOFFHAND/ALF_ISOFFHAND attack routing in stateprovider.zs) and
			// slot membership (read by the psprite layer choice below and by the
			// psprite-destroy rule in TickPSprites). QuestZDoom survives that
			// split because SwitchWeaponHand is its only writer of the flag. This
			// fork added a second writer, MoveWeaponToHand, reachable from the VR
			// wheel on the RENDER thread -- outside P_Ticker -- so the two can
			// end up disagreeing.
			//
			// When they disagree the weapon sits in ReadyWeapon while still
			// flagged offhand: TickPSprites then destroys BOTH layers (neither
			// psprite's Caller matches its slot), which is the flash-then-vanish,
			// and the shot is traced from the offhand controller while the model
			// draws in the main hand.
			//
			// Assigning the flag here, rather than only reading it, makes the
			// invariant self-healing: whatever hand a weapon is actually raised
			// into, its flag now agrees before anything else can read it.
			if (weapon.bOffhandWeapon)
			{
				if (player.ReadyWeapon == weapon)
				{
					player.SetPsprite(PSP_WEAPON, null);
					player.ReadyWeapon = null;
				}
				player.OffhandWeapon = weapon;
				weapon.bOffhandWeapon = true;
				if (weapon.SisterWeapon) weapon.SisterWeapon.bOffhandWeapon = true;
				if (weapon.bTwoHanded || (player.ReadyWeapon && player.ReadyWeapon.bTwoHanded))
				{
					player.SetPsprite(PSP_WEAPON, null);
					player.ReadyWeapon = NULL;
				}
			}
			else
			{
				if (player.OffhandWeapon == weapon)
				{
					player.SetPsprite(PSP_OFFHANDWEAPON, null);
					player.OffhandWeapon = null;
				}
				player.ReadyWeapon = weapon;
				weapon.bOffhandWeapon = false;
				if (weapon.SisterWeapon) weapon.SisterWeapon.bOffhandWeapon = false;
				if (weapon.bTwoHanded || (player.OffhandWeapon && player.OffhandWeapon.bTwoHanded))
				{
					player.SetPsprite(PSP_OFFHANDWEAPON, null);
					player.OffhandWeapon = NULL;
				}
			}
			weapon.PlayUpSound(self);
			player.refire = 0;

			let wlayer = weapon.bOffhandWeapon ? PSP_OFFHANDWEAPON : PSP_WEAPON;
			let psp = player.GetPSprite(wlayer);
			if (psp) psp.y = player.cheats & CF_INSTANTWEAPSWITCH? WEAPONTOP : WEAPONBOTTOM;
			// make sure that the previous weapon's flash state is terminated.
			// When coming here from a weapon drop it may still be active.
			player.SetPsprite(PSP_FLASH, null);
			player.SetPsprite(wlayer, weapon.GetUpState());
		}
	}

	//===========================================================================
	//
	// PlayerPawn :: BestWeapon
	//
	// Returns the best weapon a player has, possibly restricted to a single
	// type of ammo.
	//
	//===========================================================================

	Weapon BestWeapon(Class<Ammo> ammotype, int hand = 0)
	{
		Weapon bestMatch = NULL;
		int bestOrder = int.max;
		Inventory item;
		bool tomed = !!FindInventory ('PowerWeaponLevel2', true);

		// Find the best weapon the player has.
		for (item = Inv; item != NULL; item = item.Inv)
		{
			let weap = Weapon(item);
			if (weap == null)
				continue;

			if (weap.bOffhandWeapon && hand == 0 ||
				!weap.bOffhandWeapon && hand == 1)
			{
				continue;
			}

			// Don't select it if it's worse than what was already found.
			if (weap.SelectionOrder > bestOrder)
				continue;

			// Don't select it if its primary fire doesn't use the desired ammo.
			if (ammotype != NULL &&
				(weap.Ammo1 == NULL ||
				 weap.Ammo1.GetClass() != ammotype))
				continue;

			// Don't select it if the Tome is active and self isn't the powered-up version.
			if (tomed && weap.SisterWeapon != NULL && weap.SisterWeapon.bPowered_Up)
				continue;

			// Don't select it if it's powered-up and the Tome is not active.
			if (!tomed && weap.bPowered_Up)
				continue;

			// Don't select it if there isn't enough ammo to use its primary fire.
			if (!(weap.bAMMO_OPTIONAL) &&
				!weap.CheckAmmo (Weapon.PrimaryFire, false))
				continue;

			// Don't select if if there isn't enough ammo as determined by the weapon's author.
			if (weap.MinSelAmmo1 > 0 && (weap.Ammo1 == NULL || weap.Ammo1.Amount < weap.MinSelAmmo1))
				continue;
			if (weap.MinSelAmmo2 > 0 && (weap.Ammo2 == NULL || weap.Ammo2.Amount < weap.MinSelAmmo2))
				continue;

			// This weapon is usable!
			bestOrder = weap.SelectionOrder;
			bestMatch = weap;
		}
		return bestMatch;
	}

	//---------------------------------------------------------------------------
	//
	// PROC P_DropWeapon
	//
	// The player died, so put the weapon away.
	//
	//---------------------------------------------------------------------------

	void DropWeapon (int hand = 0)
	{
		let player = self.player;
		if (player == null)
		{
			return;
		}
		int disableswitch = hand ? WF_OFFHANDDISABLESWITCH : WF_DISABLESWITCH;
		// Since the weapon is dropping, stop blocking switching.
		player.WeaponState &= ~disableswitch;
		Weapon weap = hand ? player.OffhandWeapon : player.ReadyWeapon;
		if ((weap != null) && (player.health > 0 || !weap.bNoDeathDeselect))
		{
			weap.OnDeselect();
			player.SetPsprite(hand ? PSP_OFFHANDWEAPON : PSP_WEAPON, weap.GetDownState());
		}
	}

	//===========================================================================
	//
	// PlayerPawn :: PickNewWeapon
	//
	// Picks a new weapon for this player. Used mostly for running out of ammo,
	// but it also works when an ACS script explicitly takes the ready weapon
	// away or the player picks up some ammo they had previously run out of.
	//
	//===========================================================================

	Weapon PickNewWeapon(Class<Ammo> ammotype, int hand = 0)
	{
		Weapon best = BestWeapon (ammotype, hand);

		if (best != NULL)
		{
			player.PendingWeapon = best;
			Weapon weapon = hand ? player.OffhandWeapon : player.ReadyWeapon;
			if (weapon != NULL)
			{
				DropWeapon(hand);
			}
			else if (player.PendingWeapon != WP_NOCHANGE)
			{
				BringUpWeapon ();
			}
		}
		return best;
	}

	//===========================================================================
	//
	// PlayerPawn :: GiveDefaultInventory
	//
	//===========================================================================

	virtual void GiveDefaultInventory ()
	{
		let player = self.player;
		if (player == NULL) return;

		// HexenArmor must always be the first item in the inventory because
		// it provides player class based protection that should not affect
		// any other protection item.
		let myclass = GetClass();
		GiveInventoryType(GetHexenArmorClass());
		let harmor = HexenArmor(FindInventory('HexenArmor', true));

		harmor.Slots[4] = self.HexenArmor[0];
		for (int i = 0; i < 4; ++i)
		{
			harmor.SlotsIncrement[i] = self.HexenArmor[i + 1];
		}

		// BasicArmor must come right after that. It should not affect any
		// other protection item as well but needs to process the damage
		// before the HexenArmor does.
		GiveInventoryType(GetBasicArmorClass());

		// Now add the items from the DECORATE definition
		let di = GetDropItems();

		while (di)
		{
			Class<Actor> ti = di.Name;
			if (ti)
			{
				let tinv = (class<Inventory>)(ti);
				if (!tinv)
				{
					Console.Printf(TEXTCOLOR_ORANGE .. "%s is not an inventory item and cannot be given to a player as start item.\n", di.Name);
				}
				else
				{
					let item = FindInventory(tinv);
					if (item != NULL)
					{
						item.Amount = clamp(
							item.Amount + (di.Amount ? di.Amount : item.default.Amount), 0, item.MaxAmount);
					}
					else
					{
						item = Inventory(Spawn(ti));
						item.bIgnoreSkill = true;	// no skill multipliers here
						item.bDropped = item.bNeverLocal = true; // Avoid possible copies.
						item.Amount = di.Amount;
						let weap = Weapon(item);
						if (weap)
						{
							// To allow better control any weapon is emptied of
							// ammo before being given to the player.
							weap.AmmoGive1 = weap.AmmoGive2 = 0;
						}
						bool res;
						Actor check;
						[res, check] = item.CallTryPickup(self);
						if (!res)
						{
							item.Destroy();
							item = NULL;
						}
						else if (check != self)
						{
							// Player was morphed. This is illegal at game start.
							// This problem is only detectable when it's too late to do something about it...
							ThrowAbortException("Cannot give morph item '%s' when starting a game!", di.Name);
						}
					}
					let weap = Weapon(item);
					if (weap != NULL && weap.CheckAmmo(Weapon.EitherFire, false))
					{
						player.ReadyWeapon = player.PendingWeapon = weap;
					}
				}
			}
			di = di.Next;
		}
	}

	//===========================================================================
	//
	// PlayerPawn :: GiveDeathmatchInventory
	//
	// Gives players items they should have in addition to their default
	// inventory when playing deathmatch. (i.e. all keys)
	//
	//===========================================================================

	virtual void GiveDeathmatchInventory()
	{
		for ( int i = 0; i < AllActorClasses.Size(); ++i)
		{
			let cls = (class<Key>)(AllActorClasses[i]);
			if (cls)
			{
				let keyobj = GetDefaultByType(cls);
				if (keyobj.special1 != 0)
				{
					GiveInventoryType(cls);
				}
			}
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override int GetMaxHealth(bool withupgrades) const
	{
		int ret = MaxHealth > 0? MaxHealth : ((Level.compatflags & COMPATF_DEHHEALTH)? 100 : deh.MaxHealth);
		if (withupgrades) ret += stamina + BonusHealth;
		return ret;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	virtual int GetTeleportFreezeTime()
	{
		if (TeleportFreezeTime <= 0) return 0;
		let item = inv;
		while (item != null)
		{
			if (item.GetNoTeleportFreeze()) return 0;
			item = item.inv;
		}
		return TeleportFreezeTime;
	}

	//===========================================================================
	//
	// G_PlayerFinishLevel
	// Called when a player completes a level.
	//
	// flags is checked for RESETINVENTORY and RESETHEALTH only.
	//
	//===========================================================================

	void PlayerFinishLevel (int mode, int flags)
	{
		// [RL0] Handle player exit behavior for voodoo zombies
		if(PlayerFlags & PF_VOODOO_ZOMBIE)
		{
			if(player.health > 0)
			{
				PlayerFlags &= ~PF_VOODOO_ZOMBIE;
			}
			else
			{
				bShootable = false;
				bKilled = true;
			}
		}
		Inventory item, next;
		let p = player;

		if (Alternative)
		{ // Undo morph
			Unmorph(self, force: true);
		}
		// 'self' will be no longer valid from here on in case of an unmorph
		let me = p.mo;

		// Strip all current powers, unless moving in a hub and the power is okay to keep.
		item = me.Inv;
		while (item != NULL)
		{
			next = item.Inv;
			if (item is 'Powerup')
			{
				if (deathmatch || ((mode != FINISH_SameHub || !item.bHUBPOWER) && !item.bPERSISTENTPOWER)) // Keep persistent powers in non-deathmatch games
				{
					item.Destroy ();
				}
			}
			item = next;
		}
		let weap = p.ReadyWeapon;
		if (weap != NULL && weap.bPOWERED_UP && p.PendingWeapon == weap.SisterWeapon)
		{
			// Unselect powered up weapons if the unpowered counterpart is pending
			p.PendingWeapon.bOffhandWeapon = false;  // remove offhand flag to avoid issues
			p.ReadyWeapon = p.PendingWeapon;
		}
		weap = p.OffhandWeapon;
		if (weap != NULL && weap.bPOWERED_UP && p.PendingWeapon == weap.SisterWeapon)
		{
			// Unselect powered up weapons if the unpowered counterpart is pending
			p.PendingWeapon.bOffhandWeapon = true;  // we force offhand here to avoid issues
			p.OffhandWeapon = p.PendingWeapon;
		}
		// reset invisibility to default
		me.RestoreRenderStyle();
		p.extralight = 0;					// cancel gun flashes
		p.fixedcolormap = PlayerInfo.NOFIXEDCOLORMAP;	// cancel ir goggles
		p.fixedlightlevel = -1;
		p.damagecount = 0; 				// no palette changes
		p.bonuscount = 0;
		p.poisoncount = 0;
		p.inventorytics = 0;

		if (mode != FINISH_SameHub)
		{
			// Take away flight and keys (and anything else with IF_INTERHUBSTRIP set)
			item = me.Inv;
			while (item != NULL)
			{
				next = item.Inv;
				if (item.InterHubAmount < 1)
				{
					item.DepleteOrDestroy ();
				}
				item = next;
			}
		}

		if (mode == FINISH_NoHub && !level.KEEPFULLINVENTORY)
		{ // Reduce all owned (visible) inventory to defined maximum interhub amount
			Array<Inventory> todelete;
			for (item = me.Inv; item != NULL; item = item.Inv)
			{
				// If the player is carrying more samples of an item than allowed, reduce amount accordingly
				if (item.bINVBAR && item.Amount > item.InterHubAmount)
				{
					item.Amount = item.InterHubAmount;
					if (level.REMOVEITEMS && !item.bUNDROPPABLE && !item.bUNCLEARABLE)
					{
						todelete.Push(item);
					}
				}
			}
			for (int i = 0; i < toDelete.Size(); i++)
			{
				let it = toDelete[i];
				if (!it.bDestroyed)
				{
					it.DepleteOrDestroy();
				}
			}
		}

		// Resets player health to default if not dead.
		if ((flags & CHANGELEVEL_RESETHEALTH) && p.playerstate != PST_DEAD)
		{
			p.health = me.health = me.SpawnHealth();
		}

		// Clears the entire inventory and gives back the defaults for starting a game
		if ((flags & CHANGELEVEL_RESETINVENTORY) && p.playerstate != PST_DEAD)
		{
			me.ClearInventory();
			me.GiveDefaultInventory();
		}
	}

	//===========================================================================
	//
	// FWeaponSlot :: PickWeapon
	//
	// Picks a weapon from this slot. If no weapon is selected in this slot,
	// or the first weapon in this slot is selected, returns the last weapon.
	// Otherwise, returns the previous weapon in this slot. This means
	// precedence is given to the last weapon in the slot, which by convention
	// is probably the strongest. Does not return weapons you have no ammo
	// for or which you do not possess.
	//
	//===========================================================================

	virtual Weapon PickWeapon(int slot, bool checkammo, int hand = 0)
	{
		int i, j;

		let player = self.player;
		int Size = player.weapons.SlotSize(slot);
		let cur_weapon = (hand == 1) ? player.OffhandWeapon : player.ReadyWeapon;
		// Does this slot even have any weapons?
		if (Size == 0)
		{
			return cur_weapon;
		}
		if (cur_weapon != null)
		{
			for (i = 0; i < Size; i++)
			{
				let weapontype = player.weapons.GetWeapon(slot, i);
				if (weapontype == cur_weapon.GetClass() ||
					(cur_weapon.bPOWERED_UP && cur_weapon.SisterWeapon != null && cur_weapon.SisterWeapon.GetClass() == weapontype))
				{
					for (j = (i == 0 ? Size - 1 : i - 1);
						j != i;
						j = (j == 0 ? Size - 1 : j - 1))
					{
						let weapontype2 = player.weapons.GetWeapon(slot, j);
						let weap = Weapon(player.mo.FindInventory(weapontype2));

						if (weap != null)
						{
							if (!checkammo || weap.CheckAmmo(Weapon.EitherFire, false))
							{
								return weap;
							}
						}
					}
				}
			}
		}
		for (i = Size - 1; i >= 0; i--)
		{
			let weapontype = player.weapons.GetWeapon(slot, i);
			let weap = Weapon(player.mo.FindInventory(weapontype));

			if (weap != null)
			{
				if (!checkammo || weap.CheckAmmo(Weapon.EitherFire, false) &&
					((weap.bOffhandWeapon && hand == 1) ||
					(!weap.bOffhandWeapon && hand == 0)))
				{
					return weap;
				}
			}
		}
		return cur_weapon;
	}

	//===========================================================================
	//
	// SwitchHand
	//
	// Move a weapon from one hand to the other.
	// The hand is where you want to move the weapon to, 0 for main hand and 1 for offhand.
	//
	//===========================================================================

	virtual void SwitchWeaponHand(int hand = 0)
	{
		let weap = hand == 0 ? player.OffhandWeapon : player.ReadyWeapon;
		if (weap != null && !weap.bNoHandSwitch && player.playerstate == PST_LIVE)
		{
			let nextweap = player.mo.PickNextWeapon(1 - hand);
			player.OffhandWeapon = player.ReadyWeapon = null;
			weap.bOffhandWeapon = hand == 1;
			player.PendingWeapon = weap;
			player.mo.BringUpWeapon();
			// nextweap must be told which hand it is going to.
			//
			// BringUpWeapon picks the slot from bOffhandWeapon, and nothing
			// sets it on nextweap -- so it lands wherever its STALE flag
			// points, frequently the hand we just filled. That overwrites the
			// weapon we moved and leaves the other hand null, and because the
			// flag now disagrees with the slot, TickPSprites destroys BOTH
			// layers on the next tic. Observed live as
			//   ready=Pistol(off) off=VR_OffhandFist(main)
			// -- each weapon in the opposite slot from its own flag.
			//
			// The null check matters too: `nextweap != weap` passes when
			// nextweap is null, so nextweap.bTwoHanded was an unguarded read.
			//
			// This is identical to QuestZDoom, which carries the same latent
			// bug -- it only shows on a small loadout, where PickNextWeapon
			// returns something already in a hand.
			if (nextweap != null && nextweap != weap && !weap.bTwoHanded && !nextweap.bTwoHanded) {
				nextweap.bOffhandWeapon = (hand != 1);
				player.PendingWeapon = nextweap;
				player.mo.BringUpWeapon();
			}
		}
	}

	static bool WeaponsMatch(Weapon a, Weapon b)
	{
		if (a == null || b == null)
		{
			return false;
		}

		if (a == b || a.GetClass() == b.GetClass() || a.SisterWeapon == b || b.SisterWeapon == a)
		{
			return true;
		}

		if (a.SisterWeapon != null && a.SisterWeapon.GetClass() == b.GetClass())
		{
			return true;
		}
		if (b.SisterWeapon != null && b.SisterWeapon.GetClass() == a.GetClass())
		{
			return true;
		}
		return false;
	}

	// exactInstance -- match `weap` against what the hands hold by POINTER
	// IDENTITY instead of WeaponsMatch's class/sister equivalence. Default
	// off, so every existing caller keeps "same class = same weapon". A
	// caller that manages distinct instances of one class -- a matched pair
	// with one in each hand, a second fist of the class already seated
	// opposite -- needs this, because for it the class test is true exactly
	// when the seat is wanted, and the reroute into SwitchWeaponHand (a no-op
	// under NOHANDSWITCH) left the instance un-seated with no report.
	virtual void MoveWeaponToHand(Weapon weap, int hand = 0, bool exactInstance = false)
	{
		if (weap == null || player.playerstate != PST_LIVE)
		{
			return;
		}

		if (weap.bNoHandSwitch && weap.bOffhandWeapon != (hand == 1))
		{
			return;
		}

		let sourceweap = hand == 1 ? player.ReadyWeapon : player.OffhandWeapon;
		if (exactInstance ? (sourceweap == weap) : WeaponsMatch(sourceweap, weap))
		{
			SwitchWeaponHand(hand);
			return;
		}

		let targetweap = hand == 1 ? player.OffhandWeapon : player.ReadyWeapon;
		if (exactInstance ? (targetweap == weap) : WeaponsMatch(targetweap, weap))
		{
			return;
		}

		let targetcurrent = hand == 1 ? player.OffhandWeapon : player.ReadyWeapon;
		weap.bOffhandWeapon = hand == 1;
		if (weap.SisterWeapon != null)
		{
			weap.SisterWeapon.bOffhandWeapon = weap.bOffhandWeapon;
		}
		player.PendingWeapon = weap;
		if (targetcurrent != null)
		{
			DropWeapon(hand);
			return;
		}
		BringUpWeapon();
	}

	//===========================================================================
	//
	// GetVRWheelInfo
	//
	// What the VR wheel's info panel should say about the entry currently under
	// the player's hand. Return newline-separated lines; the first is treated as
	// the heading and drawn larger.
	//
	// This is a hook rather than something the engine works out because the
	// interesting facts are not the engine's to know. A mod that rolls each
	// weapon its own damage, rarity, condition and upgrades holds all of that in
	// script, and an engine-side readout could only ever show the class defaults
	// -- identical for six copies of a gun that are deliberately not identical.
	//
	// hand is 0 for the main hand and 1 for the off hand, so a mod that tracks a
	// weapon per hand can say which copy this is.
	//
	// Return the empty string to let the engine draw its own fallback (the tag
	// and ammo counts). Returning "" is not an error and is the default.
	//
	//===========================================================================

	virtual String GetVRWheelInfo(Inventory item, int hand)
	{
		return "";
	}

	//===========================================================================
	//
	// FindMostRecentWeapon
	//
	// Locates the slot and index for the most recently selected weapon. If the
	// player is in the process of switching to a new weapon, that is the most
	// recently selected weapon. Otherwise, the current weapon is the most recent
	// weapon.
	//
	//===========================================================================

	bool, int, int FindMostRecentWeapon(int hand = 0)
	{
		let player = self.player;
		let weapon = hand ? player.OffhandWeapon : player.ReadyWeapon;
		if (player.PendingWeapon != WP_NOCHANGE && player.PendingWeapon != null &&
			((player.PendingWeapon.bOffhandWeapon && hand == 1) || 
			(!player.PendingWeapon.bOffhandWeapon && hand == 0)))
		{
			// Workaround for the current inability
			bool found;
			int slot;
			int index;
			[found, slot, index] = player.weapons.LocateWeapon(player.PendingWeapon.GetClass());
			return found, slot, index;
		}
		else if (weapon != null)
		{
			bool found;
			int slot;
			int index;
			[found, slot, index] = player.weapons.LocateWeapon(weapon.GetClass());
			if (!found)
			{
				// If the current weapon wasn't found and is powered up,
				// look for its non-powered up version.
				if (weapon.bPOWERED_UP && weapon.SisterWeaponType != null)
				{
					[found, slot, index] = player.weapons.LocateWeapon(weapon.SisterWeaponType);
					return found, slot, index;
				}
				return false, 0, 0;
			}
			return true, slot, index;
		}
		else
		{
			return false, 0, 0;
		}
	}

	//===========================================================================
	//
	// FWeaponSlots :: PickNextWeapon
	//
	// Returns the "next" weapon for this player. If the current weapon is not
	// in a slot, then it just returns that weapon, since there's nothing to
	// consider it relative to.
	//
	//===========================================================================
	const NUM_WEAPON_SLOTS = 10;

	virtual Weapon PickNextWeapon(int hand = 0)
	{
		let player = self.player;
		bool found;
		int startslot, startindex;
		int slotschecked = 0;

		[found, startslot, startindex] = FindMostRecentWeapon(hand);
		let weapon = hand ? player.OffhandWeapon : player.ReadyWeapon;
		if (weapon == null || found)
		{
			int slot;
			int index;

			if (weapon == null)
			{
				startslot = NUM_WEAPON_SLOTS - 1;
				startindex = player.weapons.SlotSize(startslot) - 1;
			}

			slot = startslot;
			index = startindex;
			do
			{
				if (++index >= player.weapons.SlotSize(slot))
				{
					index = 0;
					slotschecked++;
					if (++slot >= NUM_WEAPON_SLOTS)
					{
						slot = 0;
					}
				}
				let type = player.weapons.GetWeapon(slot, index);
				let weap = Weapon(FindInventory(type));
				if (weap != null && weap.CheckAmmo(Weapon.EitherFire, false) &&
					((weap.bOffhandWeapon && hand == 1) ||
					(!weap.bOffhandWeapon && hand == 0)))
				{
					return weap;
				}
			} while ((slot != startslot || index != startindex) && slotschecked <= NUM_WEAPON_SLOTS);
		}
		return weapon;
	}

	//===========================================================================
	//
	// FWeaponSlots :: PickPrevWeapon
	//
	// Returns the "previous" weapon for this player. If the current weapon is
	// not in a slot, then it just returns that weapon, since there's nothing to
	// consider it relative to.
	//
	//===========================================================================

	virtual Weapon PickPrevWeapon(int hand = 0)
	{
		let player = self.player;
		int startslot, startindex;
		bool found;
		int slotschecked = 0;

		[found, startslot, startindex] = FindMostRecentWeapon(hand);
		let weapon = hand ? player.OffhandWeapon : player.ReadyWeapon;
		if (weapon == null || found)
		{
			int slot;
			int index;

			if (weapon == null)
			{
				startslot = 0;
				startindex = 0;
			}

			slot = startslot;
			index = startindex;
			do
			{
				if (--index < 0)
				{
					slotschecked++;
					if (--slot < 0)
					{
						slot = NUM_WEAPON_SLOTS - 1;
					}
					index = player.weapons.SlotSize(slot) - 1;
				}
				let type = player.weapons.GetWeapon(slot, index);
				let weap = Weapon(FindInventory(type));
				if (weap != null && weap.CheckAmmo(Weapon.EitherFire, false) &&
					((weap.bOffhandWeapon && hand == 1) ||
					(!weap.bOffhandWeapon && hand == 0)))
				{
					return weap;
				}
			} while ((slot != startslot || index != startindex) && slotschecked <= NUM_WEAPON_SLOTS);
		}
		return weapon;
	}

	//============================================================================
	//
	// P_BobWeapon
	//
	// [RH] Moved this out of A_WeaponReady so that the weapon can bob every
	// tic and not just when A_WeaponReady is called. Not all weapons execute
	// A_WeaponReady every tic, and it looks bad if they don't bob smoothly.
	//
	// [XA] Added new bob styles and exposed bob properties. Thanks, Ryan Cordell!
	// [SP] Added new user option for bob speed
	//
	//============================================================================

	virtual Vector2 BobWeapon (double ticfrac)
	{
		Vector2 p1, p2, r;
		Vector2 result;

		let player = self.player;
		if (!player) return (0, 0);
		let weapon = player.ReadyWeapon;

		if (weapon == null || weapon.bDontBob || player.GetWBobSpeed() == 0)
		{
			return (0, 0);
		}

		// [XA] Get the current weapon's bob properties.
		int bobstyle = weapon.BobStyle;
		double BobSpeed = (weapon.BobSpeed * 128);
		double Rangex = weapon.BobRangeX;
		double Rangey = weapon.BobRangeY;

		for (int i = 0; i < 2; i++)
		{
			// Bob the weapon based on movement speed. ([SP] And user's bob speed setting)
			double angle = (BobSpeed * player.GetWBobSpeed() * 35 /	TICRATE*(player.BobTimer - 1 + i)) * (360. / 8192.);

			// [RH] Smooth transitions between bobbing and not-bobbing frames.
			// This also fixes the bug where you can "stick" a weapon off-center by
			// shooting it when it's at the peak of its swing.
			if (curbob != player.bob)
			{
				if (abs(player.bob - curbob) <= 1)
				{
					curbob = player.bob;
				}
				else
				{
					double zoom = MAX(1., abs(curbob - player.bob) / 40);
					if (curbob > player.bob)
					{
						curbob -= zoom;
					}
					else
					{
						curbob += zoom;
					}
				}
			}

			// The weapon bobbing intensity while firing can be adjusted by the player.
			double BobIntensity = (player.WeaponState & WF_WEAPONBOBBING) ? 1. : player.GetWBobFire();

			if (curbob != 0)
			{
				double bobVal = player.bob;
				if (i == 0)
				{
					bobVal = prevBob;
				}
				//[SP] Added in decorate player.viewbob checks
				double bobx = (bobVal * BobIntensity * Rangex * ViewBob);
				double boby = (bobVal * BobIntensity * Rangey * ViewBob);
				switch (bobstyle)
				{
				case Bob_Normal:
					r.X = bobx * cos(angle);
					r.Y = boby * abs(sin(angle));
					break;

				case Bob_Inverse:
					r.X = bobx*cos(angle);
					r.Y = boby * (1. - abs(sin(angle)));
					break;

				case Bob_Alpha:
					r.X = bobx * sin(angle);
					r.Y = boby * abs(sin(angle));
					break;

				case Bob_InverseAlpha:
					r.X = bobx * sin(angle);
					r.Y = boby * (1. - abs(sin(angle)));
					break;

				case Bob_Smooth:
					r.X = bobx*cos(angle);
					r.Y = 0.5f * (boby * (1. - (cos(angle * 2))));
					break;

				case Bob_InverseSmooth:
					r.X = bobx*cos(angle);
					r.Y = 0.5f * (boby * (1. + (cos(angle * 2))));
				}
			}
			else
			{
				r = (0, 0);
			}
			if (i == 0) p1 = r; else p2 = r;
		}
		return p1 * (1. - ticfrac) + p2 * ticfrac;
	}

	virtual Vector3 /*translation*/ , Vector3 /*rotation*/ BobWeapon3D (double ticfrac)
	{
		Vector2 oldBob = BobWeapon(ticfrac);
		return (0, 0, 0) , ( oldBob.x / 4, oldBob.y / -4, 0);
	}

	//----------------------------------------------------------------------------
	//
	//
	//
	//----------------------------------------------------------------------------

	virtual clearscope color GetPainFlash() const
	{
		Color painFlash = GetPainFlashForType(DamageTypeReceived);
		if (painFlash == 0) painFlash = DamageFade;
		return painFlash;
	}

	//===========================================================================
	//
	// PlayerPawn :: ResetAirSupply
	//
	// Gives the player a full "tank" of air. If they had previously completely
	// run out of air, also plays the *gasp sound. Returns true if the player
	// was drowning.
	//
	//===========================================================================

	virtual bool ResetAirSupply (bool playgasp = true)
	{
		let player = self.player;
		bool wasdrowning = (player.air_finished < Level.maptime);

		if (playgasp && wasdrowning)
		{
			A_StartSound("*gasp", CHAN_VOICE);
		}
		if (Level.airsupply > 0 && AirCapacity > 0) player.air_finished = Level.maptime + int(Level.airsupply * AirCapacity);
		else player.air_finished = int.max;
		return wasdrowning;
	}

	//----------------------------------------------------------------------------
	//
	//
	//
	//----------------------------------------------------------------------------

	native clearscope static String GetPrintableDisplayName(Class<Actor> cls);
	native void CheckMusicChange();
	native void CheckEnvironment();
	native void CheckUse();
	native void CheckWeaponButtons(int hand = 0);
	native void MarkPlayerSounds();
	private native int SetupCrouchSprite(int c);
	private native clearscope Color GetPainFlashForType(Name type);
}

extend class Actor
{
	//----------------------------------------------------------------------------
	//
	// PROC A_SkullPop
	// This should really be in PlayerPawn but cannot be.
	//
	//----------------------------------------------------------------------------

	void A_SkullPop(class<PlayerChunk> skulltype = "BloodySkull")
	{
		// [GRB] Parameterized version
		if (skulltype == NULL || !(skulltype is "PlayerChunk"))
		{
			skulltype = "BloodySkull";
			if (skulltype == NULL)
				return;
		}

		bSolid = false;
		let mo = PlayerPawn(Spawn (skulltype,  Pos + (0, 0, 48), NO_REPLACE));
		//mo.target = self;
		mo.Vel.X = Random2[SkullPop]() / 128.;
		mo.Vel.Y = Random2[SkullPop]() / 128.;
		mo.Vel.Z = 2. + (Random[SkullPop]() / 1024.);
		// Attach player mobj to bloody skull
		let player = self.player;
		self.player = NULL;
		mo.ObtainInventory (self);
		mo.player = player;
		mo.health = health;
		mo.Angle = Angle;
		if (player != NULL)
		{
			player.mo = mo;
			player.damagecount = 32;
		}
		for (int i = 0; i < MAXPLAYERS; ++i)
		{
			if (playeringame[i] && players[i].camera == self)
			{
				players[i].camera = mo;
			}
		}
	}
}

class PlayerChunk : PlayerPawn
{
	Default
	{
		+NOSKIN
		-SOLID
		-SHOOTABLE
		-PICKUP
		-NOTDMATCH
		-FRIENDLY
		-SLIDESONWALLS
		-CANPUSHWALLS
		-FLOORCLIP
		-WINDTHRUST
		-TELESTOMP
	}
}

class PSprite : Object native play
{
	enum PSPLayers
	{
		STRIFEHANDS = -1,
		WEAPON = 1,
		FLASH = 1000,
		OFFHANDWEAPON = 1000000,
		TARGETCENTER = 0x7fffffff - 2,
		TARGETLEFT,
		TARGETRIGHT,
	};

	native readonly State CurState;
	native Actor Caller;
	native readonly PSprite Next;
	native readonly PlayerInfo Owner;
	native SpriteID Sprite;
	native int Frame;
	// RS fork -- direct model frame addressing. Frame above is a SPRITE letter
	// index and caps at 29 (MAX_SPRITE_FRAMES); these address a model frame
	// directly, so a 75-frame mesh is fully reachable. -1 = stock behaviour.
	// ModelFrameLerp in 0..1 blends ModelFrame -> ModelFrameNext explicitly,
	// instead of the renderer deriving a blend from state tics.
	native int ModelFrame;
	native int ModelFrameNext;
	native float ModelFrameLerp;

	// RS fork -- PER-PART frame addressing. The three fields above are ONE
	// number applied to EVERY sub-model in a MODELDEF stack; these address one
	// sub-model each, by its model index, and override the scalar for that
	// index only.
	//
	// This is what lets a gun be animated as PARTS instead of as poses. Declare
	// the body, slide, magazine and hand as separate models in MODELDEF, then
	// drive them independently -- the slide can travel with the hand racking it
	// while the magazine is already gone -- without pre-baking a frame for
	// every combination of every part's position. It is the only articulation
	// MD3 can express, and it needs no bones, no rig and no IQM.
	//
	// ModelPartHidden removes a part from the draw entirely, which is what a
	// magazine being out of the gun actually is.
	//
	// All -1 (and hidden all false) = inactive, and every existing weapon is
	// exactly that, so nothing that does not opt in changes at all.
	//
	// 12 is DPSprite::RS_MODEL_PARTS; parts past that index fall through to the
	// scalar path rather than failing. Indices outside 0..11 are ignored.
	native int ModelFramePart[12];
	native int ModelFrameNextPart[12];
	native float ModelFrameLerpPart[12];
	native bool ModelPartHidden[12];

	// Put every part back on the scalar path. Worth calling when a weapon is
	// deselected or a reload is aborted: a psprite layer is reused across
	// weapon switches and these fields are serialised, so a part left driven
	// stays driven -- a slide locked back on a gun that is no longer in your
	// hand, and no state anywhere saying why.
	void ClearModelParts()
	{
		for (int i = 0; i < 12; i++)
		{
			ModelFramePart[i]     = -1;
			ModelFrameNextPart[i] = -1;
			ModelFrameLerpPart[i] = -1.0;
			ModelPartHidden[i]    = false;
		}
	}

	// Drive one part. `lerp` below 0 leaves the part on whatever blend the
	// layer already had; 0..1 blends frame -> next explicitly, which is what
	// makes a hand-driven slide travel rather than snap between poses.
	void SetModelPart(int part, int frame, int next = -1, double lerp = -1.0)
	{
		if (part < 0 || part >= 12) return;
		ModelFramePart[part]     = frame;
		ModelFrameNextPart[part] = next;
		ModelFrameLerpPart[part] = lerp;
	}

	// Scrub one part along its own frame strip. `t` is 0..1 across frames
	// `first`..`last`, which is the shape a pull gesture wants: hand travel in,
	// continuous part position out.
	void SetModelPartScrub(int part, int first, int last, double t)
	{
		if (part < 0 || part >= 12) return;
		if (t < 0.0) t = 0.0;
		if (t > 1.0) t = 1.0;

		int span = last - first;
		double exact = first + span * t;
		int lo = int(exact);
		// Clamped so the final frame never blends toward one past the end,
		// which FMD3Model::RenderFrame would reject and draw as nothing.
		int hi = (span >= 0) ? min(lo + 1, last) : max(lo - 1, last);

		ModelFramePart[part]     = lo;
		ModelFrameNextPart[part] = hi;
		ModelFrameLerpPart[part] = exact - lo;
	}

	// RS fork -- PER-SURFACE frame addressing. One level finer than the part
	// arrays above, and the level a gun actually needs.
	//
	// A "part" above is a whole sub-model of a MODELDEF stack -- right for a
	// magazine or a hand shipped as their own .md3, wrong for a slide, which
	// is a SURFACE inside the pistol's own mesh alongside the frame, hammer
	// and trigger. Same for a pump, a revolver cylinder, an ejecting shell.
	// Those surfaces are already animated in the mesh; nothing could address
	// them.
	//
	// SIXTEEN SLOTS, matched on (model, surface). A slot with SurfOvModel < 0
	// is free. Sparse rather than a grid because most weapons drive nothing
	// and a rectangular model x surface table would be state on every psprite
	// for a feature almost nothing uses.
	//
	// SURFACES ARE ADDRESSED BY INDEX and that is forced, not preferred: a
	// third of the model library names its surfaces "Cube", "Untitled" or
	// "pCylinder10", and some models repeat a name. Run `modelsurfaces` in the
	// console to print each loaded model's surfaces with their indices.
	native int SurfOvModel[16];
	native int SurfOvSurface[16];
	native int SurfOvFrame[16];
	native int SurfOvNext[16];
	native float SurfOvLerp[16];
	native bool SurfOvHidden[16];

	// DISPLAY-RATE MOTION. Set these instead of the frame/next/lerp triple and
	// the renderer smooths the part across every drawn frame rather than
	// stepping it once per tic.
	//
	// UNITS: A FRACTIONAL FRAME INDEX. 3.5 means halfway between the model's
	// own frame 3 and frame 4. Not map units, not a 0..1 fraction of travel,
	// not seconds. Nothing downstream can check this -- state it, or a value
	// crossing the boundary in the wrong unit becomes motion that merely looks
	// a bit wrong.
	//
	// SurfOvPosPrev is engine bookkeeping, shifted once per tic before any
	// script runs. Do not write it; ScrubModelSurface seeds it on the first
	// call so a part does not fly in from nowhere.
	//
	// Negative means unused, and then the explicit triple above is what draws.
	native float SurfOvPos[16];
	native float SurfOvPosPrev[16];

	// The slot already driving this surface, or a free one, or -1 when all
	// sixteen are taken. Reusing the existing slot is what stops a per-tic
	// caller -- which is what a hand-driven part is -- from filling the table
	// on its second frame.
	int FindModelSurfaceSlot(int model, int surface)
	{
		if (model < 0 || surface < 0) return -1;
		int free = -1;
		for (int i = 0; i < 16; i++)
		{
			if (SurfOvModel[i] == model && SurfOvSurface[i] == surface) return i;
			if (free < 0 && SurfOvModel[i] < 0) free = i;
		}
		return free;
	}

	// Pin one surface to an exact pose. `lerp` below 0 leaves the surface on
	// whatever blend the layer already had; 0..1 blends frame -> next.
	//
	// EXACT, AND NOT SMOOTHED -- SurfOvPos is cleared, so the part is where it
	// is told and does not ease toward it. That is what a held pose wants: a
	// slide locked back on an empty gun should already be back on the frame
	// the magazine left, not drifting there over the next tic.
	void SetModelSurface(int model, int surface, int frame, int next = -1, double lerp = -1.0)
	{
		int i = FindModelSurfaceSlot(model, surface);
		if (i < 0) return;
		SurfOvModel[i]   = model;
		SurfOvSurface[i] = surface;
		SurfOvFrame[i]   = frame;
		SurfOvNext[i]    = next;
		SurfOvLerp[i]    = lerp;
		SurfOvPos[i]     = -1.0;   // explicit path
		SurfOvPosPrev[i] = -1.0;
	}

	// Scrub one surface along its own frame strip. `t` is 0..1 across frames
	// `first`..`last` -- hand travel in, continuous part position out. This is
	// the one that makes a slide feel racked rather than triggered.
	//
	// SMOOTHED. Only a fractional frame index is written; the renderer blends
	// it against last tic's on every drawn frame. Script runs at 35 Hz and a
	// headset draws at 90 or 120, so without that a hand-driven part steps
	// three times a tic no matter how smoothly the hand moves.
	void ScrubModelSurface(int model, int surface, int first, int last, double t)
	{
		if (t < 0.0) t = 0.0;
		if (t > 1.0) t = 1.0;

		int i = FindModelSurfaceSlot(model, surface);
		if (i < 0) return;

		double pos = first + (last - first) * t;
		if (pos < 0.0) pos = 0.0;

		// SEED THE HISTORY WHEN THE SLOT IS NEW, or the part's first drawn
		// frame interpolates from the -1 sentinel and it snaps in from before
		// the start of the mesh. A part that has only just started moving has
		// not moved yet.
		bool fresh = (SurfOvModel[i] != model || SurfOvSurface[i] != surface || SurfOvPos[i] < 0.0);

		SurfOvModel[i]   = model;
		SurfOvSurface[i] = surface;
		SurfOvHidden[i]  = false;
		SurfOvPos[i]     = pos;
		if (fresh) SurfOvPosPrev[i] = pos;
	}

	// Take a surface out of the draw entirely. This is what a magazine being
	// out of the gun actually is.
	void HideModelSurface(int model, int surface, bool hide = true)
	{
		int i = FindModelSurfaceSlot(model, surface);
		if (i < 0) return;
		SurfOvModel[i]   = model;
		SurfOvSurface[i] = surface;
		SurfOvHidden[i]  = hide;
	}

	// Release every surface back to the model's own animation. Call this when
	// a weapon is deselected or a reload is abandoned: psprite layers are
	// reused across weapon switches and these fields are saved, so a surface
	// left driven stays driven -- a slide locked back on a gun you are no
	// longer holding, with nothing anywhere saying why.
	void ClearModelSurfaces()
	{
		for (int i = 0; i < 16; i++)
		{
			SurfOvModel[i]   = -1;
			SurfOvSurface[i] = -1;
			SurfOvFrame[i]   = -1;
			SurfOvNext[i]    = -1;
			SurfOvLerp[i]    = -1.0;
			SurfOvHidden[i]  = false;
			SurfOvPos[i]     = -1.0;
			SurfOvPosPrev[i] = -1.0;
		}
	}

	// Hide this layer without touching the weapon behind it. The weapon keeps
	// its states, damage and slot; only the drawing stops.
	native bool NoDraw;

	// Draw this layer at a bone of another layer's model. AnchorLayer is the
	// layer id to follow, AnchorBone the bone name on it; this layer's own
	// offsets then apply relative to that bone. The anchored layer must have a
	// HIGHER id than its target, because psprites are drawn in id order and the
	// target's bones are not known until it has been drawn.
	// MDL_FOLLOWBODY (MODELDEF keyword "FollowBody"): where on the BODY this
	// layer sits and which way the body faces. Map units, body axes -- X
	// forward, Y right, Z up.
	//
	// A worn thing must be a PSPRITE to occlude against held weapons at all: a
	// world model is drawn in a separate pass, so a hand or a gun passes
	// straight through it and no content can fix that.
	native Vector3 BodyOfs;
	native double BodyYaw;

	native int AnchorLayer;
	native Name AnchorBone;

	// Where on that bone to sit, and facing which way -- in the BONE's frame,
	// so "an inch along the grip" stays an inch along the grip whichever way
	// the weapon is pointing. Ignored unless the layer is actually anchored.
	// AnchorAngles is (yaw, pitch, roll).
	native Vector3 AnchorOfs;
	native Vector3 AnchorAngles;

	// Where a bone of a DRAWN weapon actually is, as an offset from that
	// weapon's origin, in the model's own axes (+X along the barrel, +Z up) and
	// in map units. Zero when that bone was not drawn this frame.
	//
	// Turn it into a world position by rotating it with the hand that holds the
	// weapon: pos = AttackPos + fwd*x + right*y + up*z. That is what a grab
	// point should be tested against, instead of a distance somebody guessed --
	// a guess is wrong the moment the weapon is rescaled, and it fails silently.
	//
	// A bone is only published if something asked for it, so anchor a layer to
	// it (or to any bone on that model) first.
	// Where THIS layer's anchored bone resolved to: an offset from that weapon's
	// origin, in the model's own axes (+X along the barrel, +Z up) and in map
	// units. AnchorBoneLive is false until the renderer has answered.
	//
	// Turn it into a world position by rotating with the hand holding the
	// weapon: pos = AttackPos + fwd*x + right*y + up*z.
	//
	// Read from the psprite, NOT from a shared lookup. The renderer writes this
	// field on the layer that made the request; a script reaching into the
	// renderer's own table instead is a cross-thread read while that table is
	// being written, and it crashes with no message at the exact moment a hand
	// reaches for the part.
	native readonly Vector3 AnchorBonePos;
	native readonly bool AnchorBoneLive;

	// The same bone as a WORLD position, in the frame AttackPos and OffhandPos
	// use. A grab test is then just:
	//
	//   if (psp.AnchorBoneLive &&
	//       (psp.AnchorBoneWorld - player.mo.OffhandPos).Length() < grabRadius)
	//
	// with no basis to rebuild and nothing to rotate by hand. Zero, and
	// AnchorBoneLive false, when the bone was not drawn this frame.
	native readonly Vector3 AnchorBoneWorld;

	// The same bone's orientation as (yaw, pitch, roll) in degrees, in the
	// playsim's own convention -- so an actor spawned to replace a bone-driven
	// part can be given the exact angle that part was drawn at, at any weapon
	// tilt. Script cannot derive this itself: Quat exposes no vector-rotate,
	// and rebuilding the weapon's basis by hand is the same trap
	// AnchorBoneWorld exists to avoid.
	native readonly Vector3 AnchorBoneAngles;
	//native readonly int RenderStyle;	had to be blocked because the internal representation was not ok. Renderstyle is still pending a proper solution.
	native readonly int ID;
	native Bool processPending;
	native double x;
	native double y;
	native double oldx;
	native double oldy;
	native Vector2 baseScale;
	native Vector2 pivot;
	native Vector2 scale;
	native double rotation;
	native int HAlign, VAlign;
	native Vector2 Coord0;		// [MC] Not the actual coordinates. Just the offsets by A_OverlayVertexOffset.
	native Vector2 Coord1;
	native Vector2 Coord2;
	native Vector2 Coord3;
	native double alpha;
	native Bool firstTic;
	native bool InterpolateTic;
	native int Tics;
	native TranslationID Translation;
	// RS fork 2026-08-08 -- tint/glow the HELD 3D WEAPON MODEL.
	//   Tint multiplies the model's skin (0xffffffff = off)
	//   Glow  adds on top          (0 = off)
	// Per-layer, so mainhand and offhand tint independently.
	native Color Tint;
	native Color Glow;
	native bool bAddWeapon;
	native bool bAddBob;
	native bool bPowDouble;
	native bool bCVarFast;
	native bool bFlip;
	native bool bMirror;
	native bool bPlayerTranslated;
	native bool bPivotPercent;
	native bool bInterpolate;

	native void SetState(State newstate, bool pending = false);

	//------------------------------------------------------------------------
	//
	//
	//
	//------------------------------------------------------------------------

	void Tick()
	{
		if (processPending)
		{
			if (Caller)
			{
				Caller.PSpriteTick(self);
				if (bDestroyed)
					return;
			}

			if (processPending)
			{
				// drop tic count and possibly change state
				if (Tics != -1)	// a -1 tic count never changes
				{
					Tics--;
					// [BC] Apply double firing speed.
					if (bPowDouble && Tics && (Owner.mo.FindInventory ("PowerDoubleFiringSpeed", true))) Tics--;
					if (!Tics && Caller != null) SetState(CurState.NextState);
				}
			}
		}
	}

	void ResetInterpolation()
	{
		oldx = x;
		oldy = y;
	}

}

enum EPlayerState
{
	PST_LIVE,	// Playing or camping.
	PST_DEAD,	// Dead on the ground, view follows killer.
	PST_REBORN,	// Ready to restart/respawn???
	PST_ENTER,	// [BC] Entered the game
	PST_GONE	// Player has left the game
}

enum EPlayerGender
{
	GENDER_MALE,
	GENDER_FEMALE,
	GENDER_NEUTRAL,
	GENDER_OTHER
}

enum EFullbrightMode
{
	FBMODE_NONE,
	FBMODE_DEFAULT,		// Use player preference for fullbright vs night vision.
	FBMODE_FULLBRIGHT,
	FBMODE_NIGHTVISION,
	FBMODE_TORCH,
}

struct PlayerInfo native play	// self is what internally is known as player_t
{
	// technically engine constants but the only part of the playsim using them is the player.
	const NOFIXEDCOLORMAP = -1;
	const NUMCOLORMAPS = 32;

	native PlayerPawn mo;
	native uint8 playerstate;
	native readonly uint buttons;
	native uint original_oldbuttons;
	native Class<PlayerPawn> cls;
	native float DesiredFOV;
	native float FOV;
	native double viewz;
	native double viewheight;
	native double deltaviewheight;
	native double bob;
	native int BobTimer;
	native vector2 vel;
	native bool centering;
	native uint8 turnticks;
	native bool resetDoomYaw;
	native bool attackdown;
	native bool ohattackdown;
	native bool usedown;
	native uint oldbuttons;
	native int health;
	native clearscope int inventorytics;
	native int CurrentPlayerClass;
	native int frags[MAXPLAYERS];
	native int fragcount;
	native int lastkilltime;
	native uint8 multicount;
	native uint8 spreecount;
	native uint WeaponState;
	native Weapon ReadyWeapon;
	native Weapon PendingWeapon;
	native Weapon OffhandWeapon;
	native PSprite psprites;
	native int cheats;
	native int timefreezer;
	native int16 refire;
	native int16 inconsistent;
	native bool waiting;
	native int killcount;
	native int itemcount;
	native int secretcount;
	native uint damagecount;
	native uint bonuscount;
	native int hazardcount;
	native int hazardinterval;
	native Name hazardtype;
	native int poisoncount;
	native Name poisontype;
	native Name poisonpaintype;
	native Actor poisoner;
	native Actor attacker;
	native int extralight;
	native int16 fixedcolormap;
	native int16 fixedlightlevel;
	native int morphtics;
	native Class<PlayerPawn>MorphedPlayerClass;
	native int MorphStyle;
	native Class<Actor> MorphExitFlash;
	native Weapon PremorphWeapon;
	native Weapon PremorphWeaponOffhand;
	native int chickenPeck;
	native int jumpTics;
	native bool onground;
	native bool keepmomentum;
	native int respawn_time;
	native Actor camera;
	native int air_finished;
	native Name LastDamageType;
	native Actor MUSINFOactor;
	native int8 MUSINFOtics;
	native bool settings_controller;
	native int8 crouching;
	native int8 crouchdir;
	native Bot bot;
	native float BlendR;
	native float BlendG;
	native float BlendB;
	native float BlendA;
	native String LogText;
	native double MinPitch;
	native double MaxPitch;
	native double crouchfactor;
	native double crouchoffset;
	native double crouchviewdelta;
	native Actor ConversationNPC;
	native Actor ConversationPC;
	native double ConversationNPCAngle;
	native bool ConversationFaceTalker;
	native @WeaponSlots weapons;
	native @UserCmd cmd;
	native readonly @UserCmd original_cmd;
	native readonly bool PlayInVR;

	native bool PoisonPlayer(Actor poisoner, Actor source, int poison);
	native void PoisonDamage(Actor source, int damage, bool playPainSound);
	native void SetPsprite(int id, State stat, bool pending = false, Actor newcaller = null);
	native void SetSafeFlash(Weapon weap, State flashstate, int index);
	native PSprite GetPSprite(int id) const;
	native PSprite FindPSprite(int id) const;
	native void SetLogNumber (int text);
	native void SetLogText (String text);
	native void SetSubtitleNumber (int text, Sound sound_id = 0);
	native bool Resurrect();

	native clearscope String GetUserName(uint charLimit = 0u) const;
	native clearscope Color GetColor() const;
	native clearscope Color GetDisplayColor() const;
	native clearscope int GetColorSet() const;
	native clearscope int GetPlayerClassNum() const;
	native clearscope int GetSkin() const;
	native clearscope int GetSkinCount() const;
	native clearscope bool GetNeverSwitch() const;
	native clearscope int GetGender() const;
	native clearscope int GetTeam() const;
	native clearscope float GetAutoaim() const;
	native clearscope bool GetNoAutostartMap() const;
	native double GetWBobSpeed() const;
	native double GetWBobFire() const;
	native double GetMoveBob() const;
	native bool GetFViewBob() const;
	native double GetStillBob() const;
	native void SetFOV(float fov);
	native int SetSkin(int skinIndex);
	native clearscope bool GetClassicFlight() const;
	native void SendPitchLimits();
	native clearscope bool HasWeaponsInSlot(int slot) const;

	native clearscope void SetFullbrightMode(EFullbrightMode mode, bool force = false);
	native ui EFullbrightMode GetFullbrightMode() const;

	native clearscope int GetAverageLatency() const;

	native clearscope static PlayerInfo GetNextPlayer(PlayerInfo p, bool noBots = false);
	native clearscope static int GetNextPlayerNumber(int pNum, bool noBots = false);

	// The actual implementation is on PlayerPawn where it can be overridden. Use that directly in the future.
	deprecated("3.7", "MorphPlayer() should be used on a PlayerPawn object") bool MorphPlayer(PlayerInfo activator, class<PlayerPawn> spawnType, int duration, EMorphFlags style, class<Actor> enterFlash = "TeleportFog", class<Actor> exitFlash = "TeleportFog")
	{
		return mo ? mo.MorphPlayer(activator, spawnType, duration, style, enterFlash, exitFlash) : false;
	}

	// This somehow got its arguments mixed up. 'self' should have been the player to be unmorphed, not the activator
	deprecated("3.7", "UndoPlayerMorph() should be used on a PlayerPawn object") bool UndoPlayerMorph(PlayerInfo player, EMorphFlags unmorphFlags = 0, bool force = false)
	{
		return player.mo ? player.mo.UndoPlayerMorph(self, unmorphFlags, force) : false;
	}

	deprecated("3.7", "DropWeapon() should be used on a PlayerPawn object") void DropWeapon()
	{
		if (mo != null)
		{
			mo.DropWeapon();
		}
	}

	deprecated("3.7", "BringUpWeapon() should be used on a PlayerPawn object") void BringUpWeapon()
	{
		if (mo) mo.BringUpWeapon();
	}

	clearscope bool IsTotallyFrozen() const
	{
		return
			gamestate == GS_TITLELEVEL ||
			(cheats & CF_TOTALLYFROZEN) ||
			mo.isFrozen();
	}

	void Uncrouch()
	{
		if (crouchfactor != 1)
		{
			crouchfactor = 1;
			crouchoffset = 0;
			crouchdir = 0;
			crouching = 0;
			crouchviewdelta = 0;
			viewheight = mo.ViewHeight;
		}
	}

	clearscope int fragSum () const
	{
		int i;
		int allfrags = 0;
		int playernum = mo.PlayerNumber();

		for (i = 0; i < MAXPLAYERS; i++)
		{
			if (playeringame[i]
				&& i!=playernum)
			{
				allfrags += frags[i];
			}
		}

		// JDC hack - negative frags.
		allfrags -= frags[playernum];
		return allfrags;
	}

	double GetDeltaViewHeight()
	{
		return (mo.ViewHeight + crouchviewdelta - viewheight) / 8;
	}

}

struct PlayerClass native
{
	native class<Actor> Type;
	native uint Flags;
	native Array<int> Skins;

	native bool CheckSkin(int skin);
	native void EnumColorsets(out Array<int> data);
	native Name GetColorsetName(int setnum);
}

struct PlayerSkin native
{
	native readonly String		SkinName;
	native readonly String		Face;
	native readonly uint8		gender;
	native readonly uint8		range0start;
	native readonly uint8		range0end;
	native readonly bool		othergame;
	native readonly Vector2		Scale;
	native readonly int			sprite;
	native readonly int			crouchsprite;
	native readonly int			namespc;
};

struct Team native
{
	const NOTEAM = 255;
	const MAX = 16;

	native String mName;

	native static bool IsValid(uint teamIndex);
	native play static bool ChangeTeam(uint playerNumber, uint newTeamIndex);

	native Color GetPlayerColor() const;
	native int GetTextColor() const;
	native TextureID GetLogo() const;
	native string GetLogoName() const;
	native bool AllowsCustomPlayerColor() const;
}
